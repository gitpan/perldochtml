<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<html>
<head>
<meta content="text/html; charset=ISO-8859-1" http-equiv="Content-Type">
<meta content="text/css" http-equiv="Content-Style-Type">
<meta content="Pod::HTML 0.43" name="GENERATOR">
<title>perlbot</title>
</head>
<body>
<a name="Pod_TOP_OF_PAGE"></a>
<table width="100%">
<tr><td align="left" width="1%"><a href="perlbug.html">Next:<br>perlbug</a></td><td align="left" width="1%"><a href="perlboot.html">Previous:<br>perlboot</a></td><td width="90%">&nbsp;</td><td align="right" width="1%"><a class="POD_NAVLILNK" href="podtoc.html">[Table&nbsp;of&nbsp;Contents]</a></td><td align="right" width="1%"><a  href="podindex.html">[Index]</a></td></tr>
</table>
<h1 class="POD_TITLE">perlbot</h1>
<hr>
<ul>
<li><a href="#NAME">NAME</a>
<li><a href="#DESCRIPTION">DESCRIPTION</a>
<li><a href="#OO_SCALING_TIPS">OO SCALING TIPS</a>
<li><a href="#INSTANCE_VARIABLES">INSTANCE VARIABLES</a>
<li><a href="#SCALAR_INSTANCE_VARI">SCALAR INSTANCE VARIABLES</a>
<li><a href="#INSTANCE_VARIABLE_IN">INSTANCE VARIABLE INHERITANCE</a>
<li><a href="#OBJECT_RELATIONSHIPS">OBJECT RELATIONSHIPS</a>
<li><a href="#OVERRIDING_SUPERCLAS">OVERRIDING SUPERCLASS METHODS</a>
<li><a href="#USING_RELATIONSHIP_W">USING RELATIONSHIP WITH SDBM</a>
<li><a href="#THINKING_OF_CODE_REU">THINKING OF CODE REUSE</a>
<li><a href="#CLASS_CONTEXT_AND_TH">CLASS CONTEXT AND THE OBJECT</a>
<li><a href="#INHERITING_A_CONSTRU">INHERITING A CONSTRUCTOR</a>
<li><a href="#DELEGATION">DELEGATION</a>
</ul>
<hr>
<h2 class="POD_HEAD1"><a name="NAME">NAME</a></h2>
<p class="POD_TEXT">
perlbot - Bag'o Object Tricks (the BOT)
</p>
<h2 class="POD_HEAD1"><a name="DESCRIPTION">DESCRIPTION</a></h2>
<p class="POD_TEXT">
The following collection of tricks and hints is intended to whet curious
appetites about such things as the use of instance variables and the
mechanics of object and class relationships.  The reader is encouraged to
consult relevant textbooks for discussion of Object Oriented definitions and
methodology.  This is not intended as a tutorial for object-oriented
programming or as a comprehensive guide to Perl's object oriented features,
nor should it be construed as a style guide.
</p>
<p class="POD_TEXT">
The Perl motto still holds:  There's more than one way to do it.
</p>
<h2 class="POD_HEAD1"><a name="OO_SCALING_TIPS">OO SCALING TIPS</a></h2>
<ol class="POD_LIST">
<li class="POD_ITEM"><a name="_"></a>

Do not attempt to verify the type of $self.  That'll break if the class is
inherited, when the type of $self is valid but its package isn't what you
expect.  See rule 5.
<li class="POD_ITEM"><a name="_1"></a>

If an object-oriented (OO) or indirect-object (IO) syntax was used, then the
object is probably the correct type and there's no need to become paranoid
about it.  Perl isn't a paranoid language anyway.  If people subvert the OO
or IO syntax then they probably know what they're doing and you should let
them do it.  See rule 1.
<li class="POD_ITEM"><a name="_2"></a>

Use the two-argument form of bless().  Let a subclass use your constructor.
See the <a class="POD_LINK" href="#INHERITING_A_CONSTRU">INHERITING A CONSTRUCTOR</a> entry elsewhere in this document.
<li class="POD_ITEM"><a name="_3"></a>

The subclass is allowed to know things about its immediate superclass, the
superclass is allowed to know nothing about a subclass.
<li class="POD_ITEM"><a name="_4"></a>

Don't be trigger happy with inheritance.  A &quot;using&quot;, &quot;containing&quot;, or
&quot;delegation&quot; relationship (some sort of aggregation, at least) is often more
appropriate.  See the <a class="POD_LINK" href="#OBJECT_RELATIONSHIPS">OBJECT RELATIONSHIPS</a> entry elsewhere in this document, the <a class="POD_LINK" href="#USING_RELATIONSHIP_W">USING RELATIONSHIP WITH SDBM</a> entry elsewhere in this document,
and the section on <a class="POD_LINK" href="#DELEGATION">DELEGATION</a> elsewhere in this document.
<li class="POD_ITEM"><a name="_5"></a>

The object is the namespace.  Make package globals accessible via the
object.  This will remove the guess work about the symbol's home package.
See the <a class="POD_LINK" href="#CLASS_CONTEXT_AND_TH">CLASS CONTEXT AND THE OBJECT</a> entry elsewhere in this document.
<li class="POD_ITEM"><a name="_6"></a>

IO syntax is certainly less noisy, but it is also prone to ambiguities that
can cause difficult-to-find bugs.  Allow people to use the sure-thing OO
syntax, even if you don't like it.
<li class="POD_ITEM"><a name="_7"></a>

Do not use function-call syntax on a method.  You're going to be bitten
someday.  Someone might move that method into a superclass and your code
will be broken.  On top of that you're feeding the paranoia in rule 2.
<li class="POD_ITEM"><a name="_8"></a>

Don't assume you know the home package of a method.  You're making it
difficult for someone to override that method.  See the <a class="POD_LINK" href="#THINKING_OF_CODE_REU">THINKING OF CODE REUSE</a> entry elsewhere in this document.
</ol>
<h2 class="POD_HEAD1"><a name="INSTANCE_VARIABLES">INSTANCE VARIABLES</a></h2>
<pre class="POD_TEXT">
An anonymous array or anonymous hash can be used to hold instance
variables.  Named parameters are also demonstrated.

	package Foo;

	sub new {
		my $type = shift;
		my %params = @_;
		my $self = {};
		$self-&gt;{'High'} = $params{'High'};
		$self-&gt;{'Low'}  = $params{'Low'};
		bless $self, $type;
	}
</pre>
<pre>
	package Bar;

	sub new {
		my $type = shift;
		my %params = @_;
		my $self = [];
		$self-&gt;[0] = $params{'Left'};
		$self-&gt;[1] = $params{'Right'};
		bless $self, $type;
	}

	package main;

	$a = Foo-&gt;new( 'High' =&gt; 42, 'Low' =&gt; 11 );
	print &quot;High=$a-&gt;{'High'}\n&quot;;
	print &quot;Low=$a-&gt;{'Low'}\n&quot;;

	$b = Bar-&gt;new( 'Left' =&gt; 78, 'Right' =&gt; 40 );
	print &quot;Left=$b-&gt;[0]\n&quot;;
	print &quot;Right=$b-&gt;[1]\n&quot;;
</pre>
<h2 class="POD_HEAD1"><a name="SCALAR_INSTANCE_VARI">SCALAR INSTANCE VARIABLES</a></h2>
<pre class="POD_TEXT">
An anonymous scalar can be used when only one instance variable is needed.

	package Foo;

	sub new {
		my $type = shift;
		my $self;
		$self = shift;
		bless \$self, $type;
	}

	package main;

	$a = Foo-&gt;new( 42 );
	print &quot;a=$$a\n&quot;;
</pre>
<p></p>
<h2 class="POD_HEAD1"><a name="INSTANCE_VARIABLE_IN">INSTANCE VARIABLE INHERITANCE</a></h2>
<pre class="POD_TEXT">
This example demonstrates how one might inherit instance variables from a
superclass for inclusion in the new class.  This requires calling the
superclass's constructor and adding one's own instance variables to the new
object.

	package Bar;

	sub new {
		my $type = shift;
		my $self = {};
		$self-&gt;{'buz'} = 42;
		bless $self, $type;
	}

	package Foo;
	@ISA = qw( Bar );

	sub new {
		my $type = shift;
		my $self = Bar-&gt;new;
		$self-&gt;{'biz'} = 11;
		bless $self, $type;
	}

	package main;

	$a = Foo-&gt;new;
	print &quot;buz = &quot;, $a-&gt;{'buz'}, &quot;\n&quot;;
	print &quot;biz = &quot;, $a-&gt;{'biz'}, &quot;\n&quot;;
</pre>
<p></p>
<p></p>
<h2 class="POD_HEAD1"><a name="OBJECT_RELATIONSHIPS">OBJECT RELATIONSHIPS</a></h2>
<pre class="POD_TEXT">
The following demonstrates how one might implement &quot;containing&quot; and &quot;using&quot;
relationships between objects.

	package Bar;

	sub new {
		my $type = shift;
		my $self = {};
		$self-&gt;{'buz'} = 42;
		bless $self, $type;
	}

	package Foo;

	sub new {
		my $type = shift;
		my $self = {};
		$self-&gt;{'Bar'} = Bar-&gt;new;
		$self-&gt;{'biz'} = 11;
		bless $self, $type;
	}

	package main;

	$a = Foo-&gt;new;
	print &quot;buz = &quot;, $a-&gt;{'Bar'}-&gt;{'buz'}, &quot;\n&quot;;
	print &quot;biz = &quot;, $a-&gt;{'biz'}, &quot;\n&quot;;
</pre>
<p></p>
<p></p>
<h2 class="POD_HEAD1"><a name="OVERRIDING_SUPERCLAS">OVERRIDING SUPERCLASS METHODS</a></h2>
<pre class="POD_TEXT">
The following example demonstrates how to override a superclass method and
then call the overridden method.  The <b>SUPER</b> pseudo-class allows the
programmer to call an overridden superclass method without actually knowing
where that method is defined.

	package Buz;
	sub goo { print &quot;here's the goo\n&quot; }

	package Bar; @ISA = qw( Buz );
	sub google { print &quot;google here\n&quot; }

	package Baz;
	sub mumble { print &quot;mumbling\n&quot; }

	package Foo;
	@ISA = qw( Bar Baz );

	sub new {
		my $type = shift;
		bless [], $type;
	}
	sub grr { print &quot;grumble\n&quot; }
	sub goo {
		my $self = shift;
		$self-&gt;SUPER::goo();
	}
	sub mumble {
		my $self = shift;
		$self-&gt;SUPER::mumble();
	}
	sub google {
		my $self = shift;
		$self-&gt;SUPER::google();
	}

	package main;

	$foo = Foo-&gt;new;
	$foo-&gt;mumble;
	$foo-&gt;grr;
	$foo-&gt;goo;
	$foo-&gt;google;
</pre>
<p></p>
<h2 class="POD_HEAD1"><a name="USING_RELATIONSHIP_W">USING RELATIONSHIP WITH SDBM</a></h2>
<pre class="POD_TEXT">
This example demonstrates an interface for the SDBM class.  This creates a
&quot;using&quot; relationship between the SDBM class and the new class Mydbm.

	package Mydbm;

	require SDBM_File;
	require Tie::Hash;
	@ISA = qw( Tie::Hash );

	sub TIEHASH {
	    my $type = shift;
	    my $ref  = SDBM_File-&gt;new(@_);
	    bless {'dbm' =&gt; $ref}, $type;
	}
	sub FETCH {
	    my $self = shift;
	    my $ref  = $self-&gt;{'dbm'};
	    $ref-&gt;FETCH(@_);
	}
	sub STORE {
	    my $self = shift;
	    if (defined $_[0]){
		my $ref = $self-&gt;{'dbm'};
		$ref-&gt;STORE(@_);
	    } else {
		die &quot;Cannot STORE an undefined key in Mydbm\n&quot;;
	    }
	}

	package main;
	use Fcntl qw( O_RDWR O_CREAT );

	tie %foo, &quot;Mydbm&quot;, &quot;Sdbm&quot;, O_RDWR|O_CREAT, 0640;
	$foo{'bar'} = 123;
	print &quot;foo-bar = $foo{'bar'}\n&quot;;

	tie %bar, &quot;Mydbm&quot;, &quot;Sdbm2&quot;, O_RDWR|O_CREAT, 0640;
	$bar{'Cathy'} = 456;
	print &quot;bar-Cathy = $bar{'Cathy'}\n&quot;;
</pre>
<h2 class="POD_HEAD1"><a name="THINKING_OF_CODE_REU">THINKING OF CODE REUSE</a></h2>
<p class="POD_TEXT">
One strength of Object-Oriented languages is the ease with which old code
can use new code.  The following examples will demonstrate first how one can
hinder code reuse and then how one can promote code reuse.
</p>
<pre class="POD_TEXT">
This first example illustrates a class which uses a fully-qualified method
call to access the &quot;private&quot; method BAZ().  The second example will show
that it is impossible to override the BAZ() method.

	package FOO;

	sub new {
		my $type = shift;
		bless {}, $type;
	}
	sub bar {
		my $self = shift;
		$self-&gt;FOO::private::BAZ;
	}

	package FOO::private;

	sub BAZ {
		print &quot;in BAZ\n&quot;;
	}

	package main;

	$a = FOO-&gt;new;
	$a-&gt;bar;
</pre>
<pre class="POD_TEXT">
Now we try to override the BAZ() method.  We would like FOO::bar() to call
GOOP::BAZ(), but this cannot happen because FOO::bar() explicitly calls
FOO::private::BAZ().

	package FOO;

	sub new {
		my $type = shift;
		bless {}, $type;
	}
	sub bar {
		my $self = shift;
		$self-&gt;FOO::private::BAZ;
	}

	package FOO::private;

	sub BAZ {
		print &quot;in BAZ\n&quot;;
	}

	package GOOP;
	@ISA = qw( FOO );
	sub new {
		my $type = shift;
		bless {}, $type;
	}

	sub BAZ {
		print &quot;in GOOP::BAZ\n&quot;;
	}

	package main;

	$a = GOOP-&gt;new;
	$a-&gt;bar;
</pre>
<pre class="POD_TEXT">
To create reusable code we must modify class FOO, flattening class
FOO::private.  The next example shows a reusable class FOO which allows the
method GOOP::BAZ() to be used in place of FOO::BAZ().

	package FOO;

	sub new {
		my $type = shift;
		bless {}, $type;
	}
	sub bar {
		my $self = shift;
		$self-&gt;BAZ;
	}

	sub BAZ {
		print &quot;in BAZ\n&quot;;
	}

	package GOOP;
	@ISA = qw( FOO );

	sub new {
		my $type = shift;
		bless {}, $type;
	}
	sub BAZ {
		print &quot;in GOOP::BAZ\n&quot;;
	}

	package main;

	$a = GOOP-&gt;new;
	$a-&gt;bar;
</pre>
<h2 class="POD_HEAD1"><a name="CLASS_CONTEXT_AND_TH">CLASS CONTEXT AND THE OBJECT</a></h2>
<p class="POD_TEXT">
Use the object to solve package and class context problems.  Everything a
method needs should be available via the object or should be passed as a
parameter to the method.
</p>
<p class="POD_TEXT">
A class will sometimes have static or global data to be used by the
methods.  A subclass may want to override that data and replace it with new
data.  When this happens the superclass may not know how to find the new
copy of the data.
</p>
<pre class="POD_TEXT">
This problem can be solved by using the object to define the context of the
method.  Let the method look in the object for a reference to the data.  The
alternative is to force the method to go hunting for the data (&quot;Is it in my
class, or in a subclass?  Which subclass?&quot;), and this can be inconvenient
and will lead to hackery.  It is better just to let the object tell the
method where that data is located.

	package Bar;

	%fizzle = ( 'Password' =&gt; 'XYZZY' );

	sub new {
		my $type = shift;
		my $self = {};
		$self-&gt;{'fizzle'} = \%fizzle;
		bless $self, $type;
	}

	sub enter {
		my $self = shift;

		# Don't try to guess if we should use %Bar::fizzle
		# or %Foo::fizzle.  The object already knows which
		# we should use, so just ask it.
		#
		my $fizzle = $self-&gt;{'fizzle'};

		print &quot;The word is &quot;, $fizzle-&gt;{'Password'}, &quot;\n&quot;;
	}

	package Foo;
	@ISA = qw( Bar );

	%fizzle = ( 'Password' =&gt; 'Rumple' );

	sub new {
		my $type = shift;
		my $self = Bar-&gt;new;
		$self-&gt;{'fizzle'} = \%fizzle;
		bless $self, $type;
	}

	package main;

	$a = Bar-&gt;new;
	$b = Foo-&gt;new;
	$a-&gt;enter;
	$b-&gt;enter;
</pre>
<h2 class="POD_HEAD1"><a name="INHERITING_A_CONSTRU">INHERITING A CONSTRUCTOR</a></h2>
<pre class="POD_TEXT">
An inheritable constructor should use the second form of bless() which allows
blessing directly into a specified class.  Notice in this example that the
object will be a BAR not a FOO, even though the constructor is in class FOO.

	package FOO;

	sub new {
		my $type = shift;
		my $self = {};
		bless $self, $type;
	}

	sub baz {
		print &quot;in FOO::baz()\n&quot;;
	}

	package BAR;
	@ISA = qw(FOO);

	sub baz {
		print &quot;in BAR::baz()\n&quot;;
	}

	package main;

	$a = BAR-&gt;new;
	$a-&gt;baz;
</pre>
<h2 class="POD_HEAD1"><a name="DELEGATION">DELEGATION</a></h2>
<p class="POD_TEXT">
Some classes, such as SDBM_File, cannot be effectively subclassed because
they create foreign objects.  Such a class can be extended with some sort of
aggregation technique such as the &quot;using&quot; relationship mentioned earlier or
by delegation.
</p>
<pre class="POD_TEXT">
The following example demonstrates delegation using an AUTOLOAD() function to
perform message-forwarding.  This will allow the Mydbm object to behave
exactly like an SDBM_File object.  The Mydbm class could now extend the
behavior by adding custom FETCH() and STORE() methods, if this is desired.

	package Mydbm;

	require SDBM_File;
	require Tie::Hash;
	@ISA = qw(Tie::Hash);

	sub TIEHASH {
		my $type = shift;
		my $ref = SDBM_File-&gt;new(@_);
		bless {'delegate' =&gt; $ref};
	}

	sub AUTOLOAD {
		my $self = shift;

		# The Perl interpreter places the name of the
		# message in a variable called $AUTOLOAD.

		# DESTROY messages should never be propagated.
		return if $AUTOLOAD =~ /::DESTROY$/;

		# Remove the package name.
		$AUTOLOAD =~ s/^Mydbm:://;

		# Pass the message to the delegate.
		$self-&gt;{'delegate'}-&gt;$AUTOLOAD(@_);
	}

	package main;
	use Fcntl qw( O_RDWR O_CREAT );

	tie %foo, &quot;Mydbm&quot;, &quot;adbm&quot;, O_RDWR|O_CREAT, 0640;
	$foo{'bar'} = 123;
	print &quot;foo-bar = $foo{'bar'}\n&quot;;
</pre>
<hr>
<a href="#Pod_TOP_OF_PAGE">[Top]</a> 
Generated by Pod::HTML 0.43 on Mon Mar 19 17:34:52 2001
</body>
</html>

