<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<html>
<head>
<meta content="text/html; charset=ISO-8859-1" http-equiv="Content-Type">
<meta content="text/css" http-equiv="Content-Style-Type">
<meta content="Pod::HTML 0.43" name="GENERATOR">
<title>perldsc</title>
</head>
<body>
<a name="Pod_TOP_OF_PAGE"></a>
<table width="100%">
<tr><td align="left" width="1%"><a href="perlebcdic.html">Next:<br>perlebcdic</a></td><td align="left" width="1%"><a href="perldoc.html">Previous:<br>perldoc</a></td><td width="90%">&nbsp;</td><td align="right" width="1%"><a class="POD_NAVLILNK" href="podtoc.html">[Table&nbsp;of&nbsp;Contents]</a></td><td align="right" width="1%"><a  href="podindex.html">[Index]</a></td></tr>
</table>
<h1 class="POD_TITLE">perldsc</h1>
<hr>
<ul>
<li><a href="#NAME">NAME</a>
<li><a href="#DESCRIPTION">DESCRIPTION</a>
<li><a href="#REFERENCES">REFERENCES</a>
<li><a href="#COMMON_MISTAKES">COMMON MISTAKES</a>
<li><a href="#CAVEAT_ON_PRECEDENCE">CAVEAT ON PRECEDENCE</a>
<li><a href="#WHY_YOU_SHOULD_ALWAY">WHY YOU SHOULD ALWAYS <code>use strict</code></a>
<li><a href="#DEBUGGING">DEBUGGING</a>
<li><a href="#CODE_EXAMPLES">CODE EXAMPLES</a>
<li><a href="#ARRAYS_OF_ARRAYS">ARRAYS OF ARRAYS</a>
<ul>
<li><a href="#Declaration_of_a_ARR">Declaration of a ARRAY OF ARRAYS</a>
<li><a href="#Generation_of_a_ARRA">Generation of a ARRAY OF ARRAYS</a>
<li><a href="#Access_and_Printing_">Access and Printing of a ARRAY OF ARRAYS</a>
</ul>
<li><a href="#HASHES_OF_ARRAYS">HASHES OF ARRAYS</a>
<ul>
<li><a href="#Declaration_of_a_HAS">Declaration of a HASH OF ARRAYS</a>
<li><a href="#Generation_of_a_HASH">Generation of a HASH OF ARRAYS</a>
<li><a href="#Access_and_Printing_1">Access and Printing of a HASH OF ARRAYS</a>
</ul>
<li><a href="#ARRAYS_OF_HASHES">ARRAYS OF HASHES</a>
<ul>
<li><a href="#Declaration_of_a_ARR1">Declaration of a ARRAY OF HASHES</a>
<li><a href="#Generation_of_a_ARRA1">Generation of a ARRAY OF HASHES</a>
<li><a href="#Access_and_Printing_2">Access and Printing of a ARRAY OF HASHES</a>
</ul>
<li><a href="#HASHES_OF_HASHES">HASHES OF HASHES</a>
<ul>
<li><a href="#Declaration_of_a_HAS1">Declaration of a HASH OF HASHES</a>
<li><a href="#Generation_of_a_HASH1">Generation of a HASH OF HASHES</a>
<li><a href="#Access_and_Printing_3">Access and Printing of a HASH OF HASHES</a>
</ul>
<li><a href="#MORE_ELABORATE_RECOR">MORE ELABORATE RECORDS</a>
<ul>
<li><a href="#Declaration_of_MORE_">Declaration of MORE ELABORATE RECORDS</a>
<li><a href="#Declaration_of_a_HAS2">Declaration of a HASH OF COMPLEX RECORDS</a>
<li><a href="#Generation_of_a_HASH2">Generation of a HASH OF COMPLEX RECORDS</a>
</ul>
<li><a href="#Database_Ties">Database Ties</a>
<li><a href="#SEE_ALSO">SEE ALSO</a>
<li><a href="#AUTHOR">AUTHOR</a>
</ul>
<hr>
<h2 class="POD_HEAD1"><a name="NAME">NAME</a></h2>
<p class="POD_TEXT">
perldsc - Perl Data Structures Cookbook
</p>
<h2 class="POD_HEAD1"><a name="DESCRIPTION">DESCRIPTION</a></h2>
<p class="POD_TEXT">
The single feature most sorely lacking in the Perl programming language
prior to its 5.0 release was complex data structures.  Even without direct
language support, some valiant programmers did manage to emulate them, but
it was hard work and not for the faint of heart.  You could occasionally
get away with the <code>$m{$AoA,$b}</code> notation borrowed from <b>awk</b> in which the
keys are actually more like a single concatenated string <code>&quot;$AoA$b&quot;</code>, but
traversal and sorting were difficult.  More desperate programmers even
hacked Perl's internal symbol table directly, a strategy that proved hard
to develop and maintain--to put it mildly.
</p>
<pre class="POD_TEXT">
The 5.0 release of Perl let us have complex data structures.  You
may now write something like this and all of a sudden, you'd have a array
with three dimensions!

    for $x (1 .. 10) {
	for $y (1 .. 10) {
	    for $z (1 .. 10) {
		$AoA[$x][$y][$z] =
		    $x ** $y + $z;
	    }
	}
    }
</pre>
<p class="POD_TEXT">
Alas, however simple this may appear, underneath it's a much more
elaborate construct than meets the eye!
</p>
<p class="POD_TEXT">
How do you print it out?  Why can't you say just <code>print @AoA</code>?  How do
you sort it?  How can you pass it to a function or get one of these back
from a function?  Is is an object?  Can you save it to disk to read
back later?  How do you access whole rows or columns of that matrix?  Do
all the values have to be numeric?
</p>
<p class="POD_TEXT">
As you see, it's quite easy to become confused.  While some small portion
of the blame for this can be attributed to the reference-based
implementation, it's really more due to a lack of existing documentation with
examples designed for the beginner.
</p>
<p class="POD_TEXT">
This document is meant to be a detailed but understandable treatment of the
many different sorts of data structures you might want to develop.  It
should also serve as a cookbook of examples.  That way, when you need to
create one of these complex data structures, you can just pinch, pilfer, or
purloin a drop-in example from here.
</p>
<p class="POD_TEXT">
Let's look at each of these possible constructs in detail.  There are separate
sections on each of the following:
</p>
<ul class="POD_LIST">
<li class="POD_ITEM"><a name="arrays_of_arrays">arrays of arrays</a>
<li class="POD_ITEM"><a name="hashes_of_arrays">hashes of arrays</a>
<li class="POD_ITEM"><a name="arrays_of_hashes">arrays of hashes</a>
<li class="POD_ITEM"><a name="hashes_of_hashes">hashes of hashes</a>
<li class="POD_ITEM"><a name="more_elaborate_const">more elaborate constructs</a>
</ul>

But for now, let's look at general issues common to all
these types of data structures.
<h2 class="POD_HEAD1"><a name="REFERENCES">REFERENCES</a></h2>
<p class="POD_TEXT">
The most important thing to understand about all data structures in Perl
-- including multidimensional arrays--is that even though they might
appear otherwise, Perl <code>@ARRAY</code>s and <code>%HASH</code>es are all internally
one-dimensional.  They can hold only scalar values (meaning a string,
number, or a reference).  They cannot directly contain other arrays or
hashes, but instead contain <i>references</i> to other arrays or hashes.
</p>
<p class="POD_TEXT">
You can't use a reference to a array or hash in quite the same way that you
would a real array or hash.  For C or C++ programmers unused to
distinguishing between arrays and pointers to the same, this can be
confusing.  If so, just think of it as the difference between a structure
and a pointer to a structure.
</p>
<pre class="POD_TEXT">
You can (and should) read more about references in the perlref(1) man
page.  Briefly, references are rather like pointers that know what they
point to.  (Objects are also a kind of reference, but we won't be needing
them right away--if ever.)  This means that when you have something which
looks to you like an access to a two-or-more-dimensional array and/or hash,
what's really going on is that the base type is
merely a one-dimensional entity that contains references to the next
level.  It's just that you can <i>use</i> it as though it were a
two-dimensional one.  This is actually the way almost all C
multidimensional arrays work as well.

    $array[7][12]			# array of arrays
    $array[7]{string}			# array of hashes
    $hash{string}[7]			# hash of arrays
    $hash{string}{'another string'}	# hash of hashes
</pre>
<pre class="POD_TEXT">
Now, because the top level contains only references, if you try to print
out your array in with a simple print() function, you'll get something
that doesn't look very nice, like this:

    @AoA = ( [2, 3], [4, 5, 7], [0] );
    print $AoA[1][2];
  7
    print @AoA;
  ARRAY(0x83c38)ARRAY(0x8b194)ARRAY(0x8b1d0)
</pre>
<p></p>
<p class="POD_TEXT">
That's because Perl doesn't (ever) implicitly dereference your variables.
If you want to get at the thing a reference is referring to, then you have
to do this yourself using either prefix typing indicators, like
<code>${$blah}</code>, <code>@{$blah}</code>, <code>@{$blah[$i]}</code>, or else postfix pointer arrows,
like <code>$a-&gt;[3]</code>, <code>$h-&gt;{fred}</code>, or even <code>$ob-&gt;method()-&gt;[3]</code>.
</p>
<h2 class="POD_HEAD1"><a name="COMMON_MISTAKES">COMMON MISTAKES</a></h2>
<pre class="POD_TEXT">
The two most common mistakes made in constructing something like
an array of arrays is either accidentally counting the number of
elements or else taking a reference to the same memory location
repeatedly.  Here's the case where you just get the count instead
of a nested array:

    for $i (1..10) {
	@array = somefunc($i);
	$AoA[$i] = @array;	# WRONG!
    }
</pre>
<pre class="POD_TEXT">
That's just the simple case of assigning an array to a scalar and getting
its element count.  If that's what you really and truly want, then you
might do well to consider being a tad more explicit about it, like this:

    for $i (1..10) {
	@array = somefunc($i);
	$counts[$i] = scalar @array;
    }
</pre>
<pre class="POD_TEXT">
Here's the case of taking a reference to the same memory location
again and again:

    for $i (1..10) {
	@array = somefunc($i);
	$AoA[$i] = \@array;	# WRONG!
    }
</pre>
<p class="POD_TEXT">
So, what's the big problem with that?  It looks right, doesn't it?
After all, I just told you that you need an array of references, so by
golly, you've made me one!
</p>
<pre class="POD_TEXT">
Unfortunately, while this is true, it's still broken.  All the references
in @AoA refer to the <i>very same place</i>, and they will therefore all hold
whatever was last in @array!  It's similar to the problem demonstrated in
the following C program:

    #include &lt;pwd.h&gt;
    main() {
	struct passwd *getpwnam(), *rp, *dp;
	rp = getpwnam(&quot;root&quot;);
	dp = getpwnam(&quot;daemon&quot;);

	printf(&quot;daemon name is %s\nroot name is %s\n&quot;,
		dp-&gt;pw_name, rp-&gt;pw_name);
    }
</pre>
<pre class="POD_TEXT">
Which will print

    daemon name is daemon
    root name is daemon
</pre>
<pre class="POD_TEXT">
The problem is that both <code>rp</code> and <code>dp</code> are pointers to the same location
in memory!  In C, you'd have to remember to malloc() yourself some new
memory.  In Perl, you'll want to use the array constructor <code>[]</code> or the
hash constructor <code>{}</code> instead.   Here's the right way to do the preceding
broken code fragments:

    for $i (1..10) {
	@array = somefunc($i);
	$AoA[$i] = [ @array ];
    }
</pre>
<p class="POD_TEXT">
The square brackets make a reference to a new array with a <i>copy</i>
of what's in @array at the time of the assignment.  This is what
you want.
</p>
<pre class="POD_TEXT">
Note that this will produce something similar, but it's
much harder to read:

    for $i (1..10) {
	@array = 0 .. $i;
	@{$AoA[$i]} = @array;
    }
</pre>
<pre class="POD_TEXT">
Is it the same?  Well, maybe so--and maybe not.  The subtle difference
is that when you assign something in square brackets, you know for sure
it's always a brand new reference with a new <i>copy</i> of the data.
Something else could be going on in this new case with the <code>@{$AoA[$i]}}</code>
dereference on the left-hand-side of the assignment.  It all depends on
whether <code>$AoA[$i]</code> had been undefined to start with, or whether it
already contained a reference.  If you had already populated @AoA with
references, as in

    $AoA[3] = \@another_array;
</pre>
<pre class="POD_TEXT">
Then the assignment with the indirection on the left-hand-side would
use the existing reference that was already there:

    @{$AoA[3]} = @array;
</pre>
<p class="POD_TEXT">
Of course, this <i>would</i> have the &quot;interesting&quot; effect of clobbering
@another_array.  (Have you ever noticed how when a programmer says
something is &quot;interesting&quot;, that rather than meaning &quot;intriguing&quot;,
they're disturbingly more apt to mean that it's &quot;annoying&quot;,
&quot;difficult&quot;, or both?  :-)
</p>
<p class="POD_TEXT">
So just remember always to use the array or hash constructors with <code>[]</code>
or <code>{}</code>, and you'll be fine, although it's not always optimally
efficient.
</p>
<pre class="POD_TEXT">
Surprisingly, the following dangerous-looking construct will
actually work out fine:

    for $i (1..10) {
        my @array = somefunc($i);
        $AoA[$i] = \@array;
    }
</pre>
<p class="POD_TEXT">
That's because my() is more of a run-time statement than it is a
compile-time declaration <i>per se</i>.  This means that the my() variable is
remade afresh each time through the loop.  So even though it <i>looks</i> as
though you stored the same variable reference each time, you actually did
not!  This is a subtle distinction that can produce more efficient code at
the risk of misleading all but the most experienced of programmers.  So I
usually advise against teaching it to beginners.  In fact, except for
passing arguments to functions, I seldom like to see the gimme-a-reference
operator (backslash) used much at all in code.  Instead, I advise
beginners that they (and most of the rest of us) should try to use the
much more easily understood constructors <code>[]</code> and <code>{}</code> instead of
relying upon lexical (or dynamic) scoping and hidden reference-counting to
do the right thing behind the scenes.
</p>
<pre class="POD_TEXT">
In summary:

    $AoA[$i] = [ @array ];	# usually best
    $AoA[$i] = \@array;		# perilous; just how my() was that array?
    @{ $AoA[$i] } = @array;	# way too tricky for most programmers
</pre>
<p></p>
<h2 class="POD_HEAD1"><a name="CAVEAT_ON_PRECEDENCE">CAVEAT ON PRECEDENCE</a></h2>
<pre class="POD_TEXT">
Speaking of things like <code>@{$AoA[$i]}</code>, the following are actually the
same thing:

    $aref-&gt;[2][2]	# clear
    $$aref[2][2]	# confusing
</pre>
<p class="POD_TEXT">
That's because Perl's precedence rules on its five prefix dereferencers
(which look like someone swearing: <code>$ @ * % &amp;</code>) make them bind more
tightly than the postfix subscripting brackets or braces!  This will no
doubt come as a great shock to the C or C++ programmer, who is quite
accustomed to using <code>*a[i]</code> to mean what's pointed to by the <i>i'th</i>
element of <code>a</code>.  That is, they first take the subscript, and only then
dereference the thing at that subscript.  That's fine in C, but this isn't C.
</p>
<p class="POD_TEXT">
The seemingly equivalent construct in Perl, <code>$$aref[$i]</code> first does
the deref of $aref, making it take $aref as a reference to an
array, and then dereference that, and finally tell you the <i>i'th</i> value
of the array pointed to by $AoA. If you wanted the C notion, you'd have to
write <code>${$AoA[$i]}</code> to force the <code>$AoA[$i]</code> to get evaluated first
before the leading <code>$</code> dereferencer.
</p>
<h2 class="POD_HEAD1"><a name="WHY_YOU_SHOULD_ALWAY">WHY YOU SHOULD ALWAYS <code>use strict</code></a></h2>
<pre class="POD_TEXT">
If this is starting to sound scarier than it's worth, relax.  Perl has
some features to help you avoid its most common pitfalls.  The best
way to avoid getting confused is to start every program like this:

    #!/usr/bin/perl -w
    use strict;
</pre>
<pre class="POD_TEXT">
This way, you'll be forced to declare all your variables with my() and
also disallow accidental &quot;symbolic dereferencing&quot;.  Therefore if you'd done
this:

    my $aref = [
	[ &quot;fred&quot;, &quot;barney&quot;, &quot;pebbles&quot;, &quot;bambam&quot;, &quot;dino&quot;, ],
	[ &quot;homer&quot;, &quot;bart&quot;, &quot;marge&quot;, &quot;maggie&quot;, ],
	[ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot;, &quot;judy&quot;, ],
    ];

    print $aref[2][2];
</pre>
<pre class="POD_TEXT">
The compiler would immediately flag that as an error <i>at compile time</i>,
because you were accidentally accessing <code>@aref</code>, an undeclared
variable, and it would thereby remind you to write instead:

    print $aref-&gt;[2][2]
</pre>
<h2 class="POD_HEAD1"><a name="DEBUGGING">DEBUGGING</a></h2>
<pre class="POD_TEXT">
Before version 5.002, the standard Perl debugger didn't do a very nice job of
printing out complex data structures.  With 5.002 or above, the
debugger includes several new features, including command line editing as
well as the <code>x</code> command to dump out complex data structures.  For
example, given the assignment to $AoA above, here's the debugger output:

    DB&lt;1&gt; x $AoA
    $AoA = ARRAY(0x13b5a0)
       0  ARRAY(0x1f0a24)
	  0  'fred'
	  1  'barney'
	  2  'pebbles'
	  3  'bambam'
	  4  'dino'
       1  ARRAY(0x13b558)
	  0  'homer'
	  1  'bart'
	  2  'marge'
	  3  'maggie'
       2  ARRAY(0x13b540)
	  0  'george'
	  1  'jane'
	  2  'elroy'
	  3  'judy'
</pre>
<h2 class="POD_HEAD1"><a name="CODE_EXAMPLES">CODE EXAMPLES</a></h2>
<p class="POD_TEXT">
Presented with little comment (these will get their own manpages someday)
here are short code examples illustrating access of various
types of data structures.
</p>
<h2 class="POD_HEAD1"><a name="ARRAYS_OF_ARRAYS">ARRAYS OF ARRAYS</a></h2>
<h3 class="POD_HEAD2"><a name="Declaration_of_a_ARR">Declaration of a ARRAY OF ARRAYS</a></h3>
<pre class="POD_VERBATIM">
 @AoA = (
        [ &quot;fred&quot;, &quot;barney&quot; ],
        [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
        [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
      );
</pre>
<h3 class="POD_HEAD2"><a name="Generation_of_a_ARRA">Generation of a ARRAY OF ARRAYS</a></h3>
<pre class="POD_VERBATIM">
 # reading from file
 while ( &lt;&gt; ) {
     push @AoA, [ split ];
 }

 # calling a function
 for $i ( 1 .. 10 ) {
     $AoA[$i] = [ somefunc($i) ];
 }

 # using temp vars
 for $i ( 1 .. 10 ) {
     @tmp = somefunc($i);
     $AoA[$i] = [ @tmp ];
 }

 # add to an existing row
 push @{ $AoA[0] }, &quot;wilma&quot;, &quot;betty&quot;;
</pre>
<h3 class="POD_HEAD2"><a name="Access_and_Printing_">Access and Printing of a ARRAY OF ARRAYS</a></h3>
<pre class="POD_VERBATIM">
 # one element
 $AoA[0][0] = &quot;Fred&quot;;

 # another element
 $AoA[1][1] =~ s/(\w)/\u$1/;

 # print the whole thing with refs
 for $aref ( @AoA ) {
     print &quot;\t [ @$aref ],\n&quot;;
 }

 # print the whole thing with indices
 for $i ( 0 .. $#AoA ) {
     print &quot;\t [ @{$AoA[$i]} ],\n&quot;;
 }

 # print the whole thing one at a time
 for $i ( 0 .. $#AoA ) {
     for $j ( 0 .. $#{ $AoA[$i] } ) {
         print &quot;elt $i $j is $AoA[$i][$j]\n&quot;;
     }
 }
</pre>
<h2 class="POD_HEAD1"><a name="HASHES_OF_ARRAYS">HASHES OF ARRAYS</a></h2>
<h3 class="POD_HEAD2"><a name="Declaration_of_a_HAS">Declaration of a HASH OF ARRAYS</a></h3>
<pre class="POD_VERBATIM">
 %HoA = (
        flintstones        =&gt; [ &quot;fred&quot;, &quot;barney&quot; ],
        jetsons            =&gt; [ &quot;george&quot;, &quot;jane&quot;, &quot;elroy&quot; ],
        simpsons           =&gt; [ &quot;homer&quot;, &quot;marge&quot;, &quot;bart&quot; ],
      );
</pre>
<h3 class="POD_HEAD2"><a name="Generation_of_a_HASH">Generation of a HASH OF ARRAYS</a></h3>
<pre class="POD_VERBATIM">
 # reading from file
 # flintstones: fred barney wilma dino
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $HoA{$1} = [ split ];
 }

 # reading from file; more temps
 # flintstones: fred barney wilma dino
 while ( $line = &lt;&gt; ) {
     ($who, $rest) = split /:\s*/, $line, 2;
     @fields = split ' ', $rest;
     $HoA{$who} = [ @fields ];
 }

 # calling a function that returns a list
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     $HoA{$group} = [ get_family($group) ];
 }

 # likewise, but using temps
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     @members = get_family($group);
     $HoA{$group} = [ @members ];
 }

 # append new members to an existing family
 push @{ $HoA{&quot;flintstones&quot;} }, &quot;wilma&quot;, &quot;betty&quot;;
</pre>
<h3 class="POD_HEAD2"><a name="Access_and_Printing_1">Access and Printing of a HASH OF ARRAYS</a></h3>
<pre class="POD_VERBATIM">
 # one element
 $HoA{flintstones}[0] = &quot;Fred&quot;;

 # another element
 $HoA{simpsons}[1] =~ s/(\w)/\u$1/;

 # print the whole thing
 foreach $family ( keys %HoA ) {
     print &quot;$family: @{ $HoA{$family} }\n&quot;
 }

 # print the whole thing with indices
 foreach $family ( keys %HoA ) {
     print &quot;family: &quot;;
     foreach $i ( 0 .. $#{ $HoA{$family} } ) {
         print &quot; $i = $HoA{$family}[$i]&quot;;
     }
     print &quot;\n&quot;;
 }

 # print the whole thing sorted by number of members
 foreach $family ( sort { @{$HoA{$b}} &lt;=&gt; @{$HoA{$a}} } keys %HoA ) {
     print &quot;$family: @{ $HoA{$family} }\n&quot;
 }

 # print the whole thing sorted by number of members and name
 foreach $family ( sort {
			    @{$HoA{$b}} &lt;=&gt; @{$HoA{$a}}
					||
				    $a cmp $b
	    } keys %HoA )
 {
     print &quot;$family: &quot;, join(&quot;, &quot;, sort @{ $HoA{$family} }), &quot;\n&quot;;
 }
</pre>
<h2 class="POD_HEAD1"><a name="ARRAYS_OF_HASHES">ARRAYS OF HASHES</a></h2>
<h3 class="POD_HEAD2"><a name="Declaration_of_a_ARR1">Declaration of a ARRAY OF HASHES</a></h3>
<pre class="POD_VERBATIM">
 @AoH = (
        {
            Lead     =&gt; &quot;fred&quot;,
            Friend   =&gt; &quot;barney&quot;,
        },
        {
            Lead     =&gt; &quot;george&quot;,
            Wife     =&gt; &quot;jane&quot;,
            Son      =&gt; &quot;elroy&quot;,
        },
        {
            Lead     =&gt; &quot;homer&quot;,
            Wife     =&gt; &quot;marge&quot;,
            Son      =&gt; &quot;bart&quot;,
        }
  );
</pre>
<h3 class="POD_HEAD2"><a name="Generation_of_a_ARRA1">Generation of a ARRAY OF HASHES</a></h3>
<pre class="POD_VERBATIM">
 # reading from file
 # format: LEAD=fred FRIEND=barney
 while ( &lt;&gt; ) {
     $rec = {};
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $rec-&gt;{$key} = $value;
     }
     push @AoH, $rec;
 }
</pre>
<pre>
 # reading from file
 # format: LEAD=fred FRIEND=barney
 # no temp
 while ( &lt;&gt; ) {
     push @AoH, { split /[\s+=]/ };
 }

 # calling a function  that returns a key/value pair list, like
 # &quot;lead&quot;,&quot;fred&quot;,&quot;daughter&quot;,&quot;pebbles&quot;
 while ( %fields = getnextpairset() ) {
     push @AoH, { %fields };
 }

 # likewise, but using no temp vars
 while (&lt;&gt;) {
     push @AoH, { parsepairs($_) };
 }

 # add key/value to an element
 $AoH[0]{pet} = &quot;dino&quot;;
 $AoH[2]{pet} = &quot;santa's little helper&quot;;
</pre>
<h3 class="POD_HEAD2"><a name="Access_and_Printing_2">Access and Printing of a ARRAY OF HASHES</a></h3>
<pre class="POD_VERBATIM">
 # one element
 $AoH[0]{lead} = &quot;fred&quot;;

 # another element
 $AoH[1]{lead} =~ s/(\w)/\u$1/;

 # print the whole thing with refs
 for $href ( @AoH ) {
     print &quot;{ &quot;;
     for $role ( keys %$href ) {
         print &quot;$role=$href-&gt;{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

 # print the whole thing with indices
 for $i ( 0 .. $#AoH ) {
     print &quot;$i is { &quot;;
     for $role ( keys %{ $AoH[$i] } ) {
         print &quot;$role=$AoH[$i]{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

 # print the whole thing one at a time
 for $i ( 0 .. $#AoH ) {
     for $role ( keys %{ $AoH[$i] } ) {
         print &quot;elt $i $role is $AoH[$i]{$role}\n&quot;;
     }
 }
</pre>
<h2 class="POD_HEAD1"><a name="HASHES_OF_HASHES">HASHES OF HASHES</a></h2>
<h3 class="POD_HEAD2"><a name="Declaration_of_a_HAS1">Declaration of a HASH OF HASHES</a></h3>
<pre class="POD_VERBATIM">
 %HoH = (
        flintstones =&gt; {
		lead      =&gt; &quot;fred&quot;,
		pal       =&gt; &quot;barney&quot;,
        },
        jetsons     =&gt; {
		lead      =&gt; &quot;george&quot;,
		wife      =&gt; &quot;jane&quot;,
		&quot;his boy&quot; =&gt; &quot;elroy&quot;,
        },
        simpsons    =&gt; {
		lead      =&gt; &quot;homer&quot;,
		wife      =&gt; &quot;marge&quot;,
		kid       =&gt; &quot;bart&quot;,
	},
 );
</pre>
<h3 class="POD_HEAD2"><a name="Generation_of_a_HASH1">Generation of a HASH OF HASHES</a></h3>
<pre class="POD_VERBATIM">
 # reading from file
 # flintstones: lead=fred pal=barney wife=wilma pet=dino
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $who = $1;
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $HoH{$who}{$key} = $value;
     }
</pre>
<pre>
 # reading from file; more temps
 while ( &lt;&gt; ) {
     next unless s/^(.*?):\s*//;
     $who = $1;
     $rec = {};
     $HoH{$who} = $rec;
     for $field ( split ) {
         ($key, $value) = split /=/, $field;
         $rec-&gt;{$key} = $value;
     }
 }

 # calling a function  that returns a key,value hash
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     $HoH{$group} = { get_family($group) };
 }

 # likewise, but using temps
 for $group ( &quot;simpsons&quot;, &quot;jetsons&quot;, &quot;flintstones&quot; ) {
     %members = get_family($group);
     $HoH{$group} = { %members };
 }

 # append new members to an existing family
 %new_folks = (
     wife =&gt; &quot;wilma&quot;,
     pet  =&gt; &quot;dino&quot;,
 );

 for $what (keys %new_folks) {
     $HoH{flintstones}{$what} = $new_folks{$what};
 }
</pre>
<h3 class="POD_HEAD2"><a name="Access_and_Printing_3">Access and Printing of a HASH OF HASHES</a></h3>
<pre class="POD_VERBATIM">
 # one element
 $HoH{flintstones}{wife} = &quot;wilma&quot;;

 # another element
 $HoH{simpsons}{lead} =~ s/(\w)/\u$1/;

 # print the whole thing
 foreach $family ( keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

 # print the whole thing  somewhat sorted
 foreach $family ( sort keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</pre>
<pre>
 # print the whole thing sorted by number of members
 foreach $family ( sort { keys %{$HoH{$b}} &lt;=&gt; keys %{$HoH{$a}} } keys %HoH ) {
     print &quot;$family: { &quot;;
     for $role ( sort keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }

 # establish a sort order (rank) for each role
 $i = 0;
 for ( qw(lead wife son daughter pal pet) ) { $rank{$_} = ++$i }

 # now print the whole thing sorted by number of members
 foreach $family ( sort { keys %{ $HoH{$b} } &lt;=&gt; keys %{ $HoH{$a} } } keys %HoH ) {
     print &quot;$family: { &quot;;
     # and print these according to rank order
     for $role ( sort { $rank{$a} &lt;=&gt; $rank{$b} }  keys %{ $HoH{$family} } ) {
         print &quot;$role=$HoH{$family}{$role} &quot;;
     }
     print &quot;}\n&quot;;
 }
</pre>
<p></p>
<h2 class="POD_HEAD1"><a name="MORE_ELABORATE_RECOR">MORE ELABORATE RECORDS</a></h2>
<h3 class="POD_HEAD2"><a name="Declaration_of_MORE_">Declaration of MORE ELABORATE RECORDS</a></h3>
<pre class="POD_TEXT">
Here's a sample showing how to create and use a record whose fields are of
many different sorts:

     $rec = {
	 TEXT      =&gt; $string,
	 SEQUENCE  =&gt; [ @old_values ],
	 LOOKUP    =&gt; { %some_table },
	 THATCODE  =&gt; \&amp;some_function,
	 THISCODE  =&gt; sub { $_[0] ** $_[1] },
	 HANDLE    =&gt; \*STDOUT,
     };

     print $rec-&gt;{TEXT};

     print $rec-&gt;{SEQUENCE}[0];
     $last = pop @ { $rec-&gt;{SEQUENCE} };

     print $rec-&gt;{LOOKUP}{&quot;key&quot;};
     ($first_k, $first_v) = each %{ $rec-&gt;{LOOKUP} };

     $answer = $rec-&gt;{THATCODE}-&gt;($arg);
     $answer = $rec-&gt;{THISCODE}-&gt;($arg1, $arg2);

     # careful of extra block braces on fh ref
     print { $rec-&gt;{HANDLE} } &quot;a string\n&quot;;

     use FileHandle;
     $rec-&gt;{HANDLE}-&gt;autoflush(1);
     $rec-&gt;{HANDLE}-&gt;print(&quot; a string\n&quot;);
</pre>
<h3 class="POD_HEAD2"><a name="Declaration_of_a_HAS2">Declaration of a HASH OF COMPLEX RECORDS</a></h3>
<pre class="POD_VERBATIM">
     %TV = (
        flintstones =&gt; {
            series   =&gt; &quot;flintstones&quot;,
            nights   =&gt; [ qw(monday thursday friday) ],
            members  =&gt; [
                { name =&gt; &quot;fred&quot;,    role =&gt; &quot;lead&quot;, age  =&gt; 36, },
                { name =&gt; &quot;wilma&quot;,   role =&gt; &quot;wife&quot;, age  =&gt; 31, },
                { name =&gt; &quot;pebbles&quot;, role =&gt; &quot;kid&quot;,  age  =&gt;  4, },
            ],
        },

        jetsons     =&gt; {
            series   =&gt; &quot;jetsons&quot;,
            nights   =&gt; [ qw(wednesday saturday) ],
            members  =&gt; [
                { name =&gt; &quot;george&quot;,  role =&gt; &quot;lead&quot;, age  =&gt; 41, },
                { name =&gt; &quot;jane&quot;,    role =&gt; &quot;wife&quot;, age  =&gt; 39, },
                { name =&gt; &quot;elroy&quot;,   role =&gt; &quot;kid&quot;,  age  =&gt;  9, },
            ],
         },

        simpsons    =&gt; {
            series   =&gt; &quot;simpsons&quot;,
            nights   =&gt; [ qw(monday) ],
            members  =&gt; [
                { name =&gt; &quot;homer&quot;, role =&gt; &quot;lead&quot;, age  =&gt; 34, },
                { name =&gt; &quot;marge&quot;, role =&gt; &quot;wife&quot;, age =&gt; 37, },
                { name =&gt; &quot;bart&quot;,  role =&gt; &quot;kid&quot;,  age  =&gt;  11, },
            ],
         },
      );
</pre>
<h3 class="POD_HEAD2"><a name="Generation_of_a_HASH2">Generation of a HASH OF COMPLEX RECORDS</a></h3>
<pre class="POD_VERBATIM">
     # reading from file
     # this is most easily done by having the file itself be
     # in the raw data format as shown above.  perl is happy
     # to parse complex data structures if declared as data, so
     # sometimes it's easiest to do that

     # here's a piece by piece build up
     $rec = {};
     $rec-&gt;{series} = &quot;flintstones&quot;;
     $rec-&gt;{nights} = [ find_days() ];

     @members = ();
     # assume this file in field=value syntax
     while (&lt;&gt;) {
         %fields = split /[\s=]+/;
         push @members, { %fields };
     }
     $rec-&gt;{members} = [ @members ];

     # now remember the whole thing
     $TV{ $rec-&gt;{series} } = $rec;

     ###########################################################
     # now, you might want to make interesting extra fields that
     # include pointers back into the same data structure so if
     # change one piece, it changes everywhere, like for example
     # if you wanted a {kids} field that was a reference
     # to an array of the kids' records without having duplicate
     # records and thus update problems.
     ###########################################################
     foreach $family (keys %TV) {
         $rec = $TV{$family}; # temp pointer
         @kids = ();
         for $person ( @{ $rec-&gt;{members} } ) {
             if ($person-&gt;{role} =~ /kid|son|daughter/) {
                 push @kids, $person;
             }
         }
         # REMEMBER: $rec and $TV{$family} point to same data!!
         $rec-&gt;{kids} = [ @kids ];
     }

     # you copied the array, but the array itself contains pointers
     # to uncopied objects. this means that if you make bart get
     # older via

     $TV{simpsons}{kids}[0]{age}++;

     # then this would also change in
     print $TV{simpsons}{members}[2]{age};

     # because $TV{simpsons}{kids}[0] and $TV{simpsons}{members}[2]
     # both point to the same underlying anonymous hash table

     # print the whole thing
     foreach $family ( keys %TV ) {
         print &quot;the $family&quot;;
         print &quot; is on during @{ $TV{$family}{nights} }\n&quot;;
         print &quot;its members are:\n&quot;;
         for $who ( @{ $TV{$family}{members} } ) {
             print &quot; $who-&gt;{name} ($who-&gt;{role}), age $who-&gt;{age}\n&quot;;
         }
         print &quot;it turns out that $TV{$family}{lead} has &quot;;
         print scalar ( @{ $TV{$family}{kids} } ), &quot; kids named &quot;;
         print join (&quot;, &quot;, map { $_-&gt;{name} } @{ $TV{$family}{kids} } );
         print &quot;\n&quot;;
     }
</pre>
<h2 class="POD_HEAD1"><a name="Database_Ties">Database Ties</a></h2>
<p class="POD_TEXT">
You cannot easily tie a multilevel data structure (such as a hash of
hashes) to a dbm file.  The first problem is that all but GDBM and
Berkeley DB have size limitations, but beyond that, you also have problems
with how references are to be represented on disk.  One experimental
module that does partially attempt to address this need is the MLDBM
module.  Check your nearest CPAN site as described in the <a class="POD_LINK" href="perlmodlib.html">perlmodlib</a> manpage for
source code to MLDBM.
</p>
<h2 class="POD_HEAD1"><a name="SEE_ALSO">SEE ALSO</a></h2>
<p class="POD_TEXT">
perlref(1), perllol(1), perldata(1), perlobj(1)
</p>
<h2 class="POD_HEAD1"><a name="AUTHOR">AUTHOR</a></h2>
<p class="POD_TEXT">
Tom Christiansen &lt;<code>tchrist@perl.com</code>&gt;
</p>
<p class="POD_TEXT">
Last update:
Wed Oct 23 04:57:50 MET DST 1996
</p>
<hr>
<a href="#Pod_TOP_OF_PAGE">[Top]</a> 
Generated by Pod::HTML 0.43 on Mon Mar 19 17:35:13 2001
</body>
</html>

