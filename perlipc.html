<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<html>
<head>
<meta content="text/html; charset=ISO-8859-1" http-equiv="Content-Type">
<meta content="text/css" http-equiv="Content-Style-Type">
<meta content="Pod::HTML 0.43" name="GENERATOR">
<title>perlipc</title>
</head>
<body>
<a name="Pod_TOP_OF_PAGE"></a>
<table width="100%">
<tr><td align="left" width="1%"><a href="perllexwarn.html">Next:<br>perllexwarn</a></td><td align="left" width="1%"><a  href="perlintern.html">Previous:<br>perlintern</a></td><td width="90%">&nbsp;</td><td align="right" width="1%"><a class="POD_NAVLILNK" href="podtoc.html">[Table&nbsp;of&nbsp;Contents]</a></td><td align="right" width="1%"><a  href="podindex.html">[Index]</a></td></tr>
</table>
<h1 class="POD_TITLE">perlipc</h1>
<hr>
<ul>
<li><a href="#NAME">NAME</a>
<li><a href="#DESCRIPTION">DESCRIPTION</a>
<li><a href="#Signals">Signals</a>
<li><a href="#Named_Pipes">Named Pipes</a>
<ul>
<li><a href="#WARNING">WARNING</a>
</ul>
<li><a href="#Using_open_for_IPC">Using open() for IPC</a>
<ul>
<li><a href="#Filehandles">Filehandles</a>
<li><a href="#Background_Processes">Background Processes</a>
<li><a href="#Complete_Dissociatio">Complete Dissociation of Child from Parent</a>
<li><a href="#Safe_Pipe_Opens">Safe Pipe Opens</a>
<li><a href="#Bidirectional_Commun">Bidirectional Communication with Another Process</a>
<li><a href="#Bidirectional_Commun1">Bidirectional Communication with Yourself</a>
</ul>
<li><a href="#Sockets_Client_Serve">Sockets: Client/Server Communication</a>
<ul>
<li><a href="#Internet_Line_Termin">Internet Line Terminators</a>
<li><a href="#Internet_TCP_Clients">Internet TCP Clients and Servers</a>
<li><a href="#Unix_Domain_TCP_Clie">Unix-Domain TCP Clients and Servers</a>
</ul>
<li><a href="#TCP_Clients_with_IO_">TCP Clients with IO::Socket</a>
<ul>
<li><a href="#A_Simple_Client">A Simple Client</a>
<li><a href="#A_Webget_Client">A Webget Client</a>
<li><a href="#Interactive_Client_w">Interactive Client with IO::Socket</a>
</ul>
<li><a href="#TCP_Servers_with_IO_">TCP Servers with IO::Socket</a>
<li><a href="#UDP_Message_Passing">UDP: Message Passing</a>
<li><a href="#SysV_IPC">SysV IPC</a>
<li><a href="#NOTES">NOTES</a>
<li><a href="#BUGS">BUGS</a>
<li><a href="#AUTHOR">AUTHOR</a>
<li><a href="#SEE_ALSO">SEE ALSO</a>
</ul>
<hr>
<h2 class="POD_HEAD1"><a name="NAME">NAME</a></h2>
<p class="POD_TEXT">
perlipc - Perl interprocess communication (signals, fifos, pipes, safe subprocesses, sockets, and semaphores)
</p>
<h2 class="POD_HEAD1"><a name="DESCRIPTION">DESCRIPTION</a></h2>
<p class="POD_TEXT">
The basic IPC facilities of Perl are built out of the good old Unix
signals, named pipes, pipe opens, the Berkeley socket routines, and SysV
IPC calls.  Each is used in slightly different situations.
</p>
<h2 class="POD_HEAD1"><a name="Signals">Signals</a></h2>
<p class="POD_TEXT">
Perl uses a simple signal handling model: the %SIG hash contains names or
references of user-installed signal handlers.  These handlers will be called
with an argument which is the name of the signal that triggered it.  A
signal may be generated intentionally from a particular keyboard sequence like
control-C or control-Z, sent to you from another process, or
triggered automatically by the kernel when special events transpire, like
a child process exiting, your process running out of stack space, or
hitting file size limit.
</p>
<pre class="POD_TEXT">
For example, to trap an interrupt signal, set up a handler like this.
Do as little as you possibly can in your handler; notice how all we do is
set a global variable and then raise an exception.  That's because on most
systems, libraries are not re-entrant; particularly, memory allocation and
I/O routines are not.  That means that doing nearly <i>anything</i> in your
handler could in theory trigger a memory fault and subsequent core dump.

    sub catch_zap {
	my $signame = shift;
	$shucks++;
	die &quot;Somebody sent me a SIG$signame&quot;;
    }
    $SIG{INT} = 'catch_zap';  # could fail in modules
    $SIG{INT} = \&amp;catch_zap;  # best strategy
</pre>
<pre class="POD_TEXT">
The names of the signals are the ones listed out by <code>kill -l</code> on your
system, or you can retrieve them from the Config module.  Set up an
@signame list indexed by number to get the name and a %signo table
indexed by name to get the number:

    use Config;
    defined $Config{sig_name} || die &quot;No sigs?&quot;;
    foreach $name (split(' ', $Config{sig_name})) {
	$signo{$name} = $i;
	$signame[$i] = $name;
	$i++;
    }
</pre>
<pre class="POD_TEXT">
So to check whether signal 17 and SIGALRM were the same, do just this:

    print &quot;signal #17 = $signame[17]\n&quot;;
    if ($signo{ALRM}) {
	print &quot;SIGALRM is $signo{ALRM}\n&quot;;
    }
</pre>
<p class="POD_TEXT">
You may also choose to assign the strings <code>'IGNORE'</code> or <code>'DEFAULT'</code> as
the handler, in which case Perl will try to discard the signal or do the
default thing.
</p>
<p class="POD_TEXT">
On most Unix platforms, the <code>CHLD</code> (sometimes also known as <code>CLD</code>) signal
has special behavior with respect to a value of <code>'IGNORE'</code>.
Setting <code>$SIG{CHLD}</code> to <code>'IGNORE'</code> on such a platform has the effect of
not creating zombie processes when the parent process fails to <code>wait()</code>
on its child processes (i.e. child processes are automatically reaped).
Calling <code>wait()</code> with <code>$SIG{CHLD}</code> set to <code>'IGNORE'</code> usually returns
<code>-1</code> on such platforms.
</p>
<pre class="POD_TEXT">
Some signals can be neither trapped nor ignored, such as
the KILL and STOP (but not the TSTP) signals.  One strategy for
temporarily ignoring signals is to use a local() statement, which will be
automatically restored once your block is exited.  (Remember that local()
values are &quot;inherited&quot; by functions called from within that block.)

    sub precious {
	local $SIG{INT} = 'IGNORE';
	&amp;more_functions;
    }
    sub more_functions {
	# interrupts still ignored, for now...
    }
</pre>
<pre class="POD_TEXT">
Sending a signal to a negative process ID means that you send the signal
to the entire Unix process-group.  This code sends a hang-up signal to all
processes in the current process group (and sets $SIG{HUP} to IGNORE so
it doesn't kill itself):

    {
	local $SIG{HUP} = 'IGNORE';
	kill HUP =&gt; -$$;
	# snazzy writing of: kill('HUP', -$$)
    }
</pre>
<pre class="POD_TEXT">
Another interesting signal to send is signal number zero.  This doesn't
actually affect another process, but instead checks whether it's alive
or has changed its UID.

    unless (kill 0 =&gt; $kid_pid) {
	warn &quot;something wicked happened to $kid_pid&quot;;
    }
</pre>
<pre class="POD_TEXT">
You might also want to employ anonymous functions for simple signal
handlers:

    $SIG{INT} = sub { die &quot;\nOutta here!\n&quot; };
</pre>
<pre class="POD_TEXT">
But that will be problematic for the more complicated handlers that need
to reinstall themselves.  Because Perl's signal mechanism is currently
based on the signal(3) function from the C library, you may sometimes be so
misfortunate as to run on systems where that function is &quot;broken&quot;, that
is, it behaves in the old unreliable SysV way rather than the newer, more
reasonable BSD and POSIX fashion.  So you'll see defensive people writing
signal handlers like this:

    sub REAPER {
	$waitedpid = wait;
	# loathe sysV: it makes us not only reinstate
	# the handler, but place it after the wait
	$SIG{CHLD} = \&amp;REAPER;
    }
    $SIG{CHLD} = \&amp;REAPER;
    # now do something that forks...
</pre>
<pre class="POD_TEXT">
or even the more elaborate:

    use POSIX &quot;:sys_wait_h&quot;;
    sub REAPER {
	my $child;
        while (($child = waitpid(-1,WNOHANG)) &gt; 0) {
	    $Kid_Status{$child} = $?;
	}
	$SIG{CHLD} = \&amp;REAPER;  # still loathe sysV
    }
    $SIG{CHLD} = \&amp;REAPER;
    # do something that forks...
</pre>
<p class="POD_TEXT">
Signal handling is also used for timeouts in Unix,   While safely
protected within an <code>eval{}</code> block, you set a signal handler to trap
alarm signals and then schedule to have one delivered to you in some
number of seconds.  Then try your blocking operation, clearing the alarm
when it's done but not before you've exited your <code>eval{}</code> block.  If it
goes off, you'll use die() to jump out of the block, much as you might
using longjmp() or throw() in other languages.
</p>
<pre class="POD_TEXT">
Here's an example:

    eval {
        local $SIG{ALRM} = sub { die &quot;alarm clock restart&quot; };
        alarm 10;
        flock(FH, 2);   # blocking write lock
        alarm 0;
    };
    if ($@ and $@ !~ /alarm clock restart/) { die }
</pre>
<p class="POD_TEXT">
If the operation being timed out is system() or qx(), this technique
is liable to generate zombies.    If this matters to you, you'll
need to do your own fork() and exec(), and kill the errant child process.
</p>
<p class="POD_TEXT">
For more complex signal handling, you might see the standard POSIX
module.  Lamentably, this is almost entirely undocumented, but
the <code>t/lib/posix.t</code> file from the Perl source distribution has some
examples in it.
</p>
<h2 class="POD_HEAD1"><a name="Named_Pipes">Named Pipes</a></h2>
<p class="POD_TEXT">
A named pipe (often referred to as a FIFO) is an old Unix IPC
mechanism for processes communicating on the same machine.  It works
just like a regular, connected anonymous pipes, except that the
processes rendezvous using a filename and don't have to be related.
</p>
<pre class="POD_TEXT">
To create a named pipe, use the Unix command mknod(1) or on some
systems, mkfifo(1).  These may not be in your normal path.

    # system return val is backwards, so &amp;&amp; not ||
    #
    $ENV{PATH} .= &quot;:/etc:/usr/etc&quot;;
    if  (      system('mknod',  $path, 'p')
	    &amp;&amp; system('mkfifo', $path) )
    {
	die &quot;mk{nod,fifo} $path failed&quot;;
    }
</pre>
<p></p>
<p class="POD_TEXT">
A fifo is convenient when you want to connect a process to an unrelated
one.  When you open a fifo, the program will block until there's something
on the other end.
</p>
<pre class="POD_TEXT">
For example, let's say you'd like to have your <code>.signature</code> file be a
named pipe that has a Perl program on the other end.  Now every time any
program (like a mailer, news reader, finger program, etc.) tries to read
from that file, the reading program will block and your program will
supply the new signature.  We'll use the pipe-checking file test <b>-p</b>
to find out whether anyone (or anything) has accidentally removed our fifo.

    chdir; # go home
    $FIFO = '.signature';
    $ENV{PATH} .= &quot;:/etc:/usr/games&quot;;

    while (1) {
	unless (-p $FIFO) {
	    unlink $FIFO;
	    system('mknod', $FIFO, 'p')
		&amp;&amp; die &quot;can't mknod $FIFO: $!&quot;;
	}

	# next line blocks until there's a reader
	open (FIFO, &quot;&gt; $FIFO&quot;) || die &quot;can't write $FIFO: $!&quot;;
	print FIFO &quot;John Smith (smith\@host.org)\n&quot;, `fortune -s`;
	close FIFO;
	sleep 2;    # to avoid dup signals
    }
</pre>
<h3 class="POD_HEAD2"><a name="WARNING">WARNING</a></h3>
<p class="POD_TEXT">
By installing Perl code to deal with signals, you're exposing yourself
to danger from two things.  First, few system library functions are
re-entrant.  If the signal interrupts while Perl is executing one function
(like malloc(3) or printf(3)), and your signal handler then calls the
same function again, you could get unpredictable behavior--often, a
core dump.  Second, Perl isn't itself re-entrant at the lowest levels.
If the signal interrupts Perl while Perl is changing its own internal
data structures, similarly unpredictable behaviour may result.
</p>
<p class="POD_TEXT">
There are two things you can do, knowing this: be paranoid or be
pragmatic.  The paranoid approach is to do as little as possible in your
signal handler.  Set an existing integer variable that already has a
value, and return.  This doesn't help you if you're in a slow system call,
which will just restart.  That means you have to <code><a class="POD_LINK" href="perlfunc.html#die_LIST">die</a></code> to longjump(3) out
of the handler.  Even this is a little cavalier for the true paranoiac,
who avoids <code><a class="POD_LINK" href="perlfunc.html#die_LIST">die</a></code> in a handler because the system <i>is</i> out to get you.
The pragmatic approach is to say ``I know the risks, but prefer the
convenience'', and to do anything you want in your signal handler,
prepared to clean up core dumps now and again.
</p>
<p class="POD_TEXT">
To forbid signal handlers altogether would bars you from
many interesting programs, including virtually everything in this manpage,
since you could no longer even write SIGCHLD handlers.
</p>
<p></p>
<h2 class="POD_HEAD1"><a name="Using_open_for_IPC">Using open() for IPC</a></h2>
<pre class="POD_TEXT">
Perl's basic open() statement can also be used for unidirectional interprocess
communication by either appending or prepending a pipe symbol to the second
argument to open().  Here's how to start something up in a child process you
intend to write to:

    open(SPOOLER, &quot;| cat -v | lpr -h 2&gt;/dev/null&quot;)
		    || die &quot;can't fork: $!&quot;;
    local $SIG{PIPE} = sub { die &quot;spooler pipe broke&quot; };
    print SPOOLER &quot;stuff\n&quot;;
    close SPOOLER || die &quot;bad spool: $! $?&quot;;
</pre>
<pre class="POD_TEXT">
And here's how to start up a child process you intend to read from:

    open(STATUS, &quot;netstat -an 2&gt;&amp;1 |&quot;)
		    || die &quot;can't fork: $!&quot;;
    while (&lt;STATUS&gt;) {
	next if /^(tcp|udp)/;
	print;
    }
    close STATUS || die &quot;bad netstat: $! $?&quot;;
</pre>
<pre class="POD_TEXT">
If one can be sure that a particular program is a Perl script that is
expecting filenames in @ARGV, the clever programmer can write something
like this:

    % program f1 &quot;cmd1|&quot; - f2 &quot;cmd2|&quot; f3 &lt; tmpfile
</pre>
<p class="POD_TEXT">
and irrespective of which shell it's called from, the Perl program will
read from the file <code>f1</code>, the process <code>cmd1</code>, standard input (<code>tmpfile</code>
in this case), the <code>f2</code> file, the <code>cmd2</code> command, and finally the <code>f3</code>
file.  Pretty nifty, eh?
</p>
<pre class="POD_TEXT">
You might notice that you could use backticks for much the
same effect as opening a pipe for reading:

    print grep { !/^(tcp|udp)/ } `netstat -an 2&gt;&amp;1`;
    die &quot;bad netstat&quot; if $?;
</pre>
<p class="POD_TEXT">
While this is true on the surface, it's much more efficient to process the
file one line or record at a time because then you don't have to read the
whole thing into memory at once.  It also gives you finer control of the
whole process, letting you to kill off the child process early if you'd
like.
</p>
<pre class="POD_TEXT">
Be careful to check both the open() and the close() return values.  If
you're <i>writing</i> to a pipe, you should also trap SIGPIPE.  Otherwise,
think of what happens when you start up a pipe to a command that doesn't
exist: the open() will in all likelihood succeed (it only reflects the
fork()'s success), but then your output will fail--spectacularly.  Perl
can't know whether the command worked because your command is actually
running in a separate process whose exec() might have failed.  Therefore,
while readers of bogus commands return just a quick end of file, writers
to bogus command will trigger a signal they'd better be prepared to
handle.  Consider:

    open(FH, &quot;|bogus&quot;)	or die &quot;can't fork: $!&quot;;
    print FH &quot;bang\n&quot;	or die &quot;can't write: $!&quot;;
    close FH		or die &quot;can't close: $!&quot;;
</pre>
<pre class="POD_TEXT">
That won't blow up until the close, and it will blow up with a SIGPIPE.
To catch it, you could use this:

    $SIG{PIPE} = 'IGNORE';
    open(FH, &quot;|bogus&quot;)  or die &quot;can't fork: $!&quot;;
    print FH &quot;bang\n&quot;   or die &quot;can't write: $!&quot;;
    close FH            or die &quot;can't close: status=$?&quot;;
</pre>
<h3 class="POD_HEAD2"><a name="Filehandles">Filehandles</a></h3>
<p class="POD_TEXT">
Both the main process and any child processes it forks share the same
STDIN, STDOUT, and STDERR filehandles.  If both processes try to access
them at once, strange things can happen.  You may also want to close
or reopen the filehandles for the child.  You can get around this by
opening your pipe with open(), but on some systems this means that the
child process cannot outlive the parent.
</p>
<h3 class="POD_HEAD2"><a name="Background_Processes">Background Processes</a></h3>
<pre class="POD_TEXT">
You can run a command in the background with:

    system(&quot;cmd &amp;&quot;);
</pre>
<p class="POD_TEXT">
The command's STDOUT and STDERR (and possibly STDIN, depending on your
shell) will be the same as the parent's.  You won't need to catch
SIGCHLD because of the double-fork taking place (see below for more
details).
</p>
<h3 class="POD_HEAD2"><a name="Complete_Dissociatio">Complete Dissociation of Child from Parent</a></h3>
<pre class="POD_TEXT">
In some cases (starting server processes, for instance) you'll want to
completely dissociate the child process from the parent.  This is
often called daemonization.  A well behaved daemon will also chdir()
to the root directory (so it doesn't prevent unmounting the filesystem
containing the directory from which it was launched) and redirect its
standard file descriptors from and to <code>/dev/null</code> (so that random
output doesn't wind up on the user's terminal).

    use POSIX 'setsid';

    sub daemonize {
	chdir '/'		or die &quot;Can't chdir to /: $!&quot;;
	open STDIN, '/dev/null' or die &quot;Can't read /dev/null: $!&quot;;
	open STDOUT, '&gt;/dev/null'
				or die &quot;Can't write to /dev/null: $!&quot;;
	defined(my $pid = fork)	or die &quot;Can't fork: $!&quot;;
	exit if $pid;
	setsid			or die &quot;Can't start a new session: $!&quot;;
	open STDERR, '&gt;&amp;STDOUT'	or die &quot;Can't dup stdout: $!&quot;;
    }
</pre>
<p class="POD_TEXT">
The fork() has to come before the setsid() to ensure that you aren't a
process group leader (the setsid() will fail if you are).  If your
system doesn't have the setsid() function, open <code>/dev/tty</code> and use the
<code>TIOCNOTTY</code> ioctl() on it instead.  See the <u>tty</u>(4) manpage for details.
</p>
<p class="POD_TEXT">
Non-Unix users should check their Your_OS::Process module for other
solutions.
</p>
<h3 class="POD_HEAD2"><a name="Safe_Pipe_Opens">Safe Pipe Opens</a></h3>
<pre class="POD_TEXT">
Another interesting approach to IPC is making your single program go
multiprocess and communicate between (or even amongst) yourselves.  The
open() function will accept a file argument of either <code>&quot;-|&quot;</code> or <code>&quot;|-&quot;</code>
to do a very interesting thing: it forks a child connected to the
filehandle you've opened.  The child is running the same program as the
parent.  This is useful for safely opening a file when running under an
assumed UID or GID, for example.  If you open a pipe <i>to</i> minus, you can
write to the filehandle you opened and your kid will find it in his
STDIN.  If you open a pipe <i>from</i> minus, you can read from the filehandle
you opened whatever your kid writes to his STDOUT.

    use English;
    my $sleep_count = 0;

    do {
	$pid = open(KID_TO_WRITE, &quot;|-&quot;);
	unless (defined $pid) {
	    warn &quot;cannot fork: $!&quot;;
	    die &quot;bailing out&quot; if $sleep_count++ &gt; 6;
	    sleep 10;
	}
    } until defined $pid;

    if ($pid) {  # parent
	print KID_TO_WRITE @some_data;
	close(KID_TO_WRITE) || warn &quot;kid exited $?&quot;;
    } else {     # child
	($EUID, $EGID) = ($UID, $GID); # suid progs only
	open (FILE, &quot;&gt; /safe/file&quot;)
	    || die &quot;can't open /safe/file: $!&quot;;
	while (&lt;STDIN&gt;) {
	    print FILE; # child's STDIN is parent's KID
	}
	exit;  # don't forget this
    }
</pre>
<p class="POD_TEXT">
Another common use for this construct is when you need to execute
something without the shell's interference.  With system(), it's
straightforward, but you can't use a pipe open or backticks safely.
That's because there's no way to stop the shell from getting its hands on
your arguments.   Instead, use lower-level control to call exec() directly.
</p>
<pre class="POD_TEXT">
Here's a safe backtick or pipe open for read:

    # add error processing as above
    $pid = open(KID_TO_READ, &quot;-|&quot;);

    if ($pid) {   # parent
	while (&lt;KID_TO_READ&gt;) {
	    # do something interesting
	}
	close(KID_TO_READ) || warn &quot;kid exited $?&quot;;

    } else {      # child
	($EUID, $EGID) = ($UID, $GID); # suid only
	exec($program, @options, @args)
	    || die &quot;can't exec program: $!&quot;;
	# NOTREACHED
    }
</pre>
<p></p>
<pre class="POD_TEXT">
And here's a safe pipe open for writing:

    # add error processing as above
    $pid = open(KID_TO_WRITE, &quot;|-&quot;);
    $SIG{ALRM} = sub { die &quot;whoops, $program pipe broke&quot; };

    if ($pid) {  # parent
	for (@data) {
	    print KID_TO_WRITE;
	}
	close(KID_TO_WRITE) || warn &quot;kid exited $?&quot;;

    } else {     # child
	($EUID, $EGID) = ($UID, $GID);
	exec($program, @options, @args)
	    || die &quot;can't exec program: $!&quot;;
	# NOTREACHED
    }
</pre>
<p class="POD_TEXT">
Note that these operations are full Unix forks, which means they may not be
correctly implemented on alien systems.  Additionally, these are not true
multithreading.  If you'd like to learn more about threading, see the
<code>modules</code> file mentioned below in the SEE ALSO section.
</p>
<h3 class="POD_HEAD2"><a name="Bidirectional_Commun">Bidirectional Communication with Another Process</a></h3>
<pre class="POD_TEXT">
While this works reasonably well for unidirectional communication, what
about bidirectional communication?  The obvious thing you'd like to do
doesn't actually work:

    open(PROG_FOR_READING_AND_WRITING, &quot;| some program |&quot;)
</pre>
<pre class="POD_TEXT">
and if you forget to use the <code>use warnings</code> pragma or the <b>-w</b> flag,
then you'll miss out entirely on the diagnostic message:

    Can't do bidirectional pipe at -e line 1.
</pre>
<p class="POD_TEXT">
If you really want to, you can use the standard open2() library function
to catch both ends.  There's also an open3() for tridirectional I/O so you
can also catch your child's STDERR, but doing so would then require an
awkward select() loop and wouldn't allow you to use normal Perl input
operations.
</p>
<p class="POD_TEXT">
If you look at its source, you'll see that open2() uses low-level
primitives like Unix pipe() and exec() calls to create all the connections.
While it might have been slightly more efficient by using socketpair(), it
would have then been even less portable than it already is.  The open2()
and open3() functions are  unlikely to work anywhere except on a Unix
system or some other one purporting to be POSIX compliant.
</p>
<pre class="POD_TEXT">
Here's an example of using open2():

    use FileHandle;
    use IPC::Open2;
    $pid = open2(*Reader, *Writer, &quot;cat -u -n&quot; );
    print Writer &quot;stuff\n&quot;;
    $got = &lt;Reader&gt;;
</pre>
<p class="POD_TEXT">
The problem with this is that Unix buffering is really going to
ruin your day.  Even though your <code>Writer</code> filehandle is auto-flushed,
and the process on the other end will get your data in a timely manner,
you can't usually do anything to force it to give it back to you
in a similarly quick fashion.  In this case, we could, because we
gave <i>cat</i> a <b>-u</b> flag to make it unbuffered.  But very few Unix
commands are designed to operate over pipes, so this seldom works
unless you yourself wrote the program on the other end of the
double-ended pipe.
</p>
<pre class="POD_TEXT">
A solution to this is the nonstandard <code>Comm.pl</code> library.  It uses
pseudo-ttys to make your program behave more reasonably:

    require 'Comm.pl';
    $ph = open_proc('cat -n');
    for (1..10) {
	print $ph &quot;a line\n&quot;;
	print &quot;got back &quot;, scalar &lt;$ph&gt;;
    }
</pre>
<p class="POD_TEXT">
This way you don't have to have control over the source code of the
program you're using.  The <code>Comm</code> library also has expect()
and interact() functions.  Find the library (and we hope its
successor <code>IPC::Chat</code>) at your nearest CPAN archive as detailed
in the SEE ALSO section below.
</p>
<p class="POD_TEXT">
The newer Expect.pm module from CPAN also addresses this kind of thing.
This module requires two other modules from CPAN: IO::Pty and IO::Stty.
It sets up a pseudo-terminal to interact with programs that insist on
using talking to the terminal device driver.  If your system is 
amongst those supported, this may be your best bet.
</p>
<h3 class="POD_HEAD2"><a name="Bidirectional_Commun1">Bidirectional Communication with Yourself</a></h3>
<pre class="POD_TEXT">
If you want, you may make low-level pipe() and fork()
to stitch this together by hand.  This example only
talks to itself, but you could reopen the appropriate
handles to STDIN and STDOUT and call other processes.

    #!/usr/bin/perl -w
    # pipe1 - bidirectional communication using two pipe pairs
    #         designed for the socketpair-challenged
    use IO::Handle;	# thousands of lines just for autoflush :-(
    pipe(PARENT_RDR, CHILD_WTR);		# XXX: failure?
    pipe(CHILD_RDR,  PARENT_WTR);		# XXX: failure?
    CHILD_WTR-&gt;autoflush(1);
    PARENT_WTR-&gt;autoflush(1);

    if ($pid = fork) {
	close PARENT_RDR; close PARENT_WTR;
	print CHILD_WTR &quot;Parent Pid $$ is sending this\n&quot;;
	chomp($line = &lt;CHILD_RDR&gt;);
	print &quot;Parent Pid $$ just read this: `$line'\n&quot;;
	close CHILD_RDR; close CHILD_WTR;
	waitpid($pid,0);
    } else {
	die &quot;cannot fork: $!&quot; unless defined $pid;
	close CHILD_RDR; close CHILD_WTR;
	chomp($line = &lt;PARENT_RDR&gt;);
	print &quot;Child Pid $$ just read this: `$line'\n&quot;;
	print PARENT_WTR &quot;Child Pid $$ is sending this\n&quot;;
	close PARENT_RDR; close PARENT_WTR;
	exit;
    }
</pre>
<pre class="POD_TEXT">
But you don't actually have to make two pipe calls.  If you 
have the socketpair() system call, it will do this all for you.

    #!/usr/bin/perl -w
    # pipe2 - bidirectional communication using socketpair
    #   &quot;the best ones always go both ways&quot;

    use Socket;
    use IO::Handle;	# thousands of lines just for autoflush :-(
    # We say AF_UNIX because although *_LOCAL is the
    # POSIX 1003.1g form of the constant, many machines
    # still don't have it.
    socketpair(CHILD, PARENT, AF_UNIX, SOCK_STREAM, PF_UNSPEC)
				or  die &quot;socketpair: $!&quot;;

    CHILD-&gt;autoflush(1);
    PARENT-&gt;autoflush(1);

    if ($pid = fork) {
	close PARENT;
	print CHILD &quot;Parent Pid $$ is sending this\n&quot;;
	chomp($line = &lt;CHILD&gt;);
	print &quot;Parent Pid $$ just read this: `$line'\n&quot;;
	close CHILD;
	waitpid($pid,0);
    } else {
	die &quot;cannot fork: $!&quot; unless defined $pid;
	close CHILD;
	chomp($line = &lt;PARENT&gt;);
	print &quot;Child Pid $$ just read this: `$line'\n&quot;;
	print PARENT &quot;Child Pid $$ is sending this\n&quot;;
	close PARENT;
	exit;
    }
</pre>
<h2 class="POD_HEAD1"><a name="Sockets_Client_Serve">Sockets: Client/Server Communication</a></h2>
<p class="POD_TEXT">
While not limited to Unix-derived operating systems (e.g., WinSock on PCs
provides socket support, as do some VMS libraries), you may not have
sockets on your system, in which case this section probably isn't going to do
you much good.  With sockets, you can do both virtual circuits (i.e., TCP
streams) and datagrams (i.e., UDP packets).  You may be able to do even more
depending on your system.
</p>
<p class="POD_TEXT">
The Perl function calls for dealing with sockets have the same names as
the corresponding system calls in C, but their arguments tend to differ
for two reasons: first, Perl filehandles work differently than C file
descriptors.  Second, Perl already knows the length of its strings, so you
don't need to pass that information.
</p>
<p class="POD_TEXT">
One of the major problems with old socket code in Perl was that it used
hard-coded values for some of the constants, which severely hurt
portability.  If you ever see code that does anything like explicitly
setting <code>$AF_INET = 2</code>, you know you're in for big trouble:  An
immeasurably superior approach is to use the <code>Socket</code> module, which more
reliably grants access to various constants and functions you'll need.
</p>
<p class="POD_TEXT">
If you're not writing a server/client for an existing protocol like
NNTP or SMTP, you should give some thought to how your server will
know when the client has finished talking, and vice-versa.  Most
protocols are based on one-line messages and responses (so one party
knows the other has finished when a &quot;\n&quot; is received) or multi-line
messages and responses that end with a period on an empty line
(&quot;\n.\n&quot; terminates a message/response).
</p>
<h3 class="POD_HEAD2"><a name="Internet_Line_Termin">Internet Line Terminators</a></h3>
<p class="POD_TEXT">
The Internet line terminator is &quot;\015\012&quot;.  Under ASCII variants of
Unix, that could usually be written as &quot;\r\n&quot;, but under other systems,
&quot;\r\n&quot; might at times be &quot;\015\015\012&quot;, &quot;\012\012\015&quot;, or something
completely different.  The standards specify writing &quot;\015\012&quot; to be
conformant (be strict in what you provide), but they also recommend
accepting a lone &quot;\012&quot; on input (but be lenient in what you require).
We haven't always been very good about that in the code in this manpage,
but unless you're on a Mac, you'll probably be ok.
</p>
<h3 class="POD_HEAD2"><a name="Internet_TCP_Clients">Internet TCP Clients and Servers</a></h3>
<p class="POD_TEXT">
Use Internet-domain sockets when you want to do client-server
communication that might extend to machines outside of your own system.
</p>
<pre class="POD_TEXT">
Here's a sample TCP client using Internet-domain sockets:

    #!/usr/bin/perl -w
    use strict;
    use Socket;
    my ($remote,$port, $iaddr, $paddr, $proto, $line);

    $remote  = shift || 'localhost';
    $port    = shift || 2345;  # random port
    if ($port =~ /\D/) { $port = getservbyname($port, 'tcp') }
    die &quot;No port&quot; unless $port;
    $iaddr   = inet_aton($remote) 		|| die &quot;no host: $remote&quot;;
    $paddr   = sockaddr_in($port, $iaddr);

    $proto   = getprotobyname('tcp');
    socket(SOCK, PF_INET, SOCK_STREAM, $proto)	|| die &quot;socket: $!&quot;;
    connect(SOCK, $paddr)    || die &quot;connect: $!&quot;;
    while (defined($line = &lt;SOCK&gt;)) {
	print $line;
    }

    close (SOCK)	    || die &quot;close: $!&quot;;
    exit;
</pre>
<pre class="POD_TEXT">
And here's a corresponding server to go along with it.  We'll
leave the address as INADDR_ANY so that the kernel can choose
the appropriate interface on multihomed hosts.  If you want sit
on a particular interface (like the external side of a gateway
or firewall machine), you should fill this in with your real address
instead.

    #!/usr/bin/perl -Tw
    use strict;
    BEGIN { $ENV{PATH} = '/usr/ucb:/bin' }
    use Socket;
    use Carp;
    my $EOL = &quot;\015\012&quot;;

    sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime, &quot;\n&quot; }

    my $port = shift || 2345;
    my $proto = getprotobyname('tcp');

    ($port) = $port =~ /^(\d+)$/                        or die &quot;invalid port&quot;;

    socket(Server, PF_INET, SOCK_STREAM, $proto)	|| die &quot;socket: $!&quot;;
    setsockopt(Server, SOL_SOCKET, SO_REUSEADDR,
					pack(&quot;l&quot;, 1)) 	|| die &quot;setsockopt: $!&quot;;
    bind(Server, sockaddr_in($port, INADDR_ANY))	|| die &quot;bind: $!&quot;;
    listen(Server,SOMAXCONN) 				|| die &quot;listen: $!&quot;;

    logmsg &quot;server started on port $port&quot;;

    my $paddr;

    $SIG{CHLD} = \&amp;REAPER;

    for ( ; $paddr = accept(Client,Server); close Client) {
	my($port,$iaddr) = sockaddr_in($paddr);
	my $name = gethostbyaddr($iaddr,AF_INET);

	logmsg &quot;connection from $name [&quot;,
		inet_ntoa($iaddr), &quot;]
		at port $port&quot;;

	print Client &quot;Hello there, $name, it's now &quot;,
			scalar localtime, $EOL;
    }
</pre>
<pre class="POD_TEXT">
And here's a multithreaded version.  It's multithreaded in that
like most typical servers, it spawns (forks) a slave server to
handle the client request so that the master server can quickly
go back to service a new client.

    #!/usr/bin/perl -Tw
    use strict;
    BEGIN { $ENV{PATH} = '/usr/ucb:/bin' }
    use Socket;
    use Carp;
    my $EOL = &quot;\015\012&quot;;

    sub spawn;  # forward declaration
    sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime, &quot;\n&quot; }

    my $port = shift || 2345;
    my $proto = getprotobyname('tcp');

    ($port) = $port =~ /^(\d+)$/                        or die &quot;invalid port&quot;;

    socket(Server, PF_INET, SOCK_STREAM, $proto)	|| die &quot;socket: $!&quot;;
    setsockopt(Server, SOL_SOCKET, SO_REUSEADDR,
					pack(&quot;l&quot;, 1)) 	|| die &quot;setsockopt: $!&quot;;
    bind(Server, sockaddr_in($port, INADDR_ANY))	|| die &quot;bind: $!&quot;;
    listen(Server,SOMAXCONN) 				|| die &quot;listen: $!&quot;;

    logmsg &quot;server started on port $port&quot;;

    my $waitedpid = 0;
    my $paddr;

    sub REAPER {
	$waitedpid = wait;
	$SIG{CHLD} = \&amp;REAPER;  # loathe sysV
	logmsg &quot;reaped $waitedpid&quot; . ($? ? &quot; with exit $?&quot; : '');
    }

    $SIG{CHLD} = \&amp;REAPER;

    for ( $waitedpid = 0;
	  ($paddr = accept(Client,Server)) || $waitedpid;
	  $waitedpid = 0, close Client)
    {
	next if $waitedpid and not $paddr;
	my($port,$iaddr) = sockaddr_in($paddr);
	my $name = gethostbyaddr($iaddr,AF_INET);

	logmsg &quot;connection from $name [&quot;,
		inet_ntoa($iaddr), &quot;]
		at port $port&quot;;

	spawn sub {
	    $|=1;
	    print &quot;Hello there, $name, it's now &quot;, scalar localtime, $EOL;
	    exec '/usr/games/fortune'		# XXX: `wrong' line terminators
		or confess &quot;can't exec fortune: $!&quot;;
	};

    }

    sub spawn {
	my $coderef = shift;

	unless (@_ == 0 &amp;&amp; $coderef &amp;&amp; ref($coderef) eq 'CODE') {
	    confess &quot;usage: spawn CODEREF&quot;;
	}

	my $pid;
	if (!defined($pid = fork)) {
	    logmsg &quot;cannot fork: $!&quot;;
	    return;
	} elsif ($pid) {
	    logmsg &quot;begat $pid&quot;;
	    return; # I'm the parent
	}
	# else I'm the child -- go spawn

	open(STDIN,  &quot;&lt;&amp;Client&quot;)   || die &quot;can't dup client to stdin&quot;;
	open(STDOUT, &quot;&gt;&amp;Client&quot;)   || die &quot;can't dup client to stdout&quot;;
	## open(STDERR, &quot;&gt;&amp;STDOUT&quot;) || die &quot;can't dup stdout to stderr&quot;;
	exit &amp;$coderef();
    }
</pre>
<p class="POD_TEXT">
This server takes the trouble to clone off a child version via fork() for
each incoming request.  That way it can handle many requests at once,
which you might not always want.  Even if you don't fork(), the listen()
will allow that many pending connections.  Forking servers have to be
particularly careful about cleaning up their dead children (called
&quot;zombies&quot; in Unix parlance), because otherwise you'll quickly fill up your
process table.
</p>
<p class="POD_TEXT">
We suggest that you use the <b>-T</b> flag to use taint checking (see the <a class="POD_LINK" href="perlsec.html">perlsec</a> manpage)
even if we aren't running setuid or setgid.  This is always a good idea
for servers and other programs run on behalf of someone else (like CGI
scripts), because it lessens the chances that people from the outside will
be able to compromise your system.
</p>
<pre class="POD_TEXT">
Let's look at another TCP client.  This one connects to the TCP &quot;time&quot;
service on a number of different machines and shows how far their clocks
differ from the system on which it's being run:

    #!/usr/bin/perl  -w
    use strict;
    use Socket;

    my $SECS_of_70_YEARS = 2208988800;
    sub ctime { scalar localtime(shift) }

    my $iaddr = gethostbyname('localhost');
    my $proto = getprotobyname('tcp');
    my $port = getservbyname('time', 'tcp');
    my $paddr = sockaddr_in(0, $iaddr);
    my($host);

    $| = 1;
    printf &quot;%-24s %8s %s\n&quot;,  &quot;localhost&quot;, 0, ctime(time());

    foreach $host (@ARGV) {
	printf &quot;%-24s &quot;, $host;
	my $hisiaddr = inet_aton($host)     || die &quot;unknown host&quot;;
	my $hispaddr = sockaddr_in($port, $hisiaddr);
	socket(SOCKET, PF_INET, SOCK_STREAM, $proto)   || die &quot;socket: $!&quot;;
	connect(SOCKET, $hispaddr)          || die &quot;bind: $!&quot;;
	my $rtime = '    ';
	read(SOCKET, $rtime, 4);
	close(SOCKET);
	my $histime = unpack(&quot;N&quot;, $rtime) - $SECS_of_70_YEARS ;
	printf &quot;%8d %s\n&quot;, $histime - time, ctime($histime);
    }
</pre>
<h3 class="POD_HEAD2"><a name="Unix_Domain_TCP_Clie">Unix-Domain TCP Clients and Servers</a></h3>
<pre class="POD_TEXT">
That's fine for Internet-domain clients and servers, but what about local
communications?  While you can use the same setup, sometimes you don't
want to.  Unix-domain sockets are local to the current host, and are often
used internally to implement pipes.  Unlike Internet domain sockets, Unix
domain sockets can show up in the file system with an ls(1) listing.

    % ls -l /dev/log
    srw-rw-rw-  1 root            0 Oct 31 07:23 /dev/log
</pre>
<pre class="POD_TEXT">
You can test for these with Perl's <b>-S</b> file test:

    unless ( -S '/dev/log' ) {
	die &quot;something's wicked with the log system&quot;;
    }
</pre>
<pre class="POD_TEXT">
Here's a sample Unix-domain client:

    #!/usr/bin/perl -w
    use Socket;
    use strict;
    my ($rendezvous, $line);

    $rendezvous = shift || '/tmp/catsock';
    socket(SOCK, PF_UNIX, SOCK_STREAM, 0)	|| die &quot;socket: $!&quot;;
    connect(SOCK, sockaddr_un($rendezvous))	|| die &quot;connect: $!&quot;;
    while (defined($line = &lt;SOCK&gt;)) {
	print $line;
    }
    exit;
</pre>
<pre class="POD_TEXT">
And here's a corresponding server.  You don't have to worry about silly
network terminators here because Unix domain sockets are guaranteed
to be on the localhost, and thus everything works right.

    #!/usr/bin/perl -Tw
    use strict;
    use Socket;
    use Carp;

    BEGIN { $ENV{PATH} = '/usr/ucb:/bin' }
    sub spawn;  # forward declaration
    sub logmsg { print &quot;$0 $$: @_ at &quot;, scalar localtime, &quot;\n&quot; }

    my $NAME = '/tmp/catsock';
    my $uaddr = sockaddr_un($NAME);
    my $proto = getprotobyname('tcp');

    socket(Server,PF_UNIX,SOCK_STREAM,0) 	|| die &quot;socket: $!&quot;;
    unlink($NAME);
    bind  (Server, $uaddr) 			|| die &quot;bind: $!&quot;;
    listen(Server,SOMAXCONN)			|| die &quot;listen: $!&quot;;

    logmsg &quot;server started on $NAME&quot;;

    my $waitedpid;

    sub REAPER {
	$waitedpid = wait;
	$SIG{CHLD} = \&amp;REAPER;  # loathe sysV
	logmsg &quot;reaped $waitedpid&quot; . ($? ? &quot; with exit $?&quot; : '');
    }

    $SIG{CHLD} = \&amp;REAPER;
</pre>
<pre>
    for ( $waitedpid = 0;
	  accept(Client,Server) || $waitedpid;
	  $waitedpid = 0, close Client)
    {
	next if $waitedpid;
	logmsg &quot;connection on $NAME&quot;;
	spawn sub {
	    print &quot;Hello there, it's now &quot;, scalar localtime, &quot;\n&quot;;
	    exec '/usr/games/fortune' or die &quot;can't exec fortune: $!&quot;;
	};
    }

    sub spawn {
	my $coderef = shift;

	unless (@_ == 0 &amp;&amp; $coderef &amp;&amp; ref($coderef) eq 'CODE') {
	    confess &quot;usage: spawn CODEREF&quot;;
	}

	my $pid;
	if (!defined($pid = fork)) {
	    logmsg &quot;cannot fork: $!&quot;;
	    return;
	} elsif ($pid) {
	    logmsg &quot;begat $pid&quot;;
	    return; # I'm the parent
	}
	# else I'm the child -- go spawn

	open(STDIN,  &quot;&lt;&amp;Client&quot;)   || die &quot;can't dup client to stdin&quot;;
	open(STDOUT, &quot;&gt;&amp;Client&quot;)   || die &quot;can't dup client to stdout&quot;;
	## open(STDERR, &quot;&gt;&amp;STDOUT&quot;) || die &quot;can't dup stdout to stderr&quot;;
	exit &amp;$coderef();
    }
</pre>
<p class="POD_TEXT">
As you see, it's remarkably similar to the Internet domain TCP server, so
much so, in fact, that we've omitted several duplicate functions--spawn(),
logmsg(), ctime(), and REAPER()--which are exactly the same as in the
other server.
</p>
<p class="POD_TEXT">
So why would you ever want to use a Unix domain socket instead of a
simpler named pipe?  Because a named pipe doesn't give you sessions.  You
can't tell one process's data from another's.  With socket programming,
you get a separate session for each client: that's why accept() takes two
arguments.
</p>
<p class="POD_TEXT">
For example, let's say that you have a long running database server daemon
that you want folks from the World Wide Web to be able to access, but only
if they go through a CGI interface.  You'd have a small, simple CGI
program that does whatever checks and logging you feel like, and then acts
as a Unix-domain client and connects to your private server.
</p>
<h2 class="POD_HEAD1"><a name="TCP_Clients_with_IO_">TCP Clients with IO::Socket</a></h2>
<p class="POD_TEXT">
For those preferring a higher-level interface to socket programming, the
IO::Socket module provides an object-oriented approach.  IO::Socket is
included as part of the standard Perl distribution as of the 5.004
release.  If you're running an earlier version of Perl, just fetch
IO::Socket from CPAN, where you'll also find modules providing easy
interfaces to the following systems: DNS, FTP, Ident (RFC 931), NIS and
NISPlus, NNTP, Ping, POP3, SMTP, SNMP, SSLeay, Telnet, and Time--just
to name a few.
</p>
<h3 class="POD_HEAD2"><a name="A_Simple_Client">A Simple Client</a></h3>
<pre class="POD_TEXT">
Here's a client that creates a TCP connection to the &quot;daytime&quot;
service at port 13 of the host name &quot;localhost&quot; and prints out everything
that the server there cares to provide.

    #!/usr/bin/perl -w
    use IO::Socket;
    $remote = IO::Socket::INET-&gt;new(
			Proto    =&gt; &quot;tcp&quot;,
			PeerAddr =&gt; &quot;localhost&quot;,
			PeerPort =&gt; &quot;daytime(13)&quot;,
		    )
		  or die &quot;cannot connect to daytime port at localhost&quot;;
    while ( &lt;$remote&gt; ) { print }
</pre>
<pre class="POD_TEXT">
When you run this program, you should get something back that
looks like this:

    Wed May 14 08:40:46 MDT 1997
</pre>
<p class="POD_TEXT">
Here are what those parameters to the <code>new</code> constructor mean:
</p>
<dl class="POD_LIST">
<dt class="POD_ITEM"><a name="Proto"><code>Proto</code></a>
<dd>
This is which protocol to use.  In this case, the socket handle returned
will be connected to a TCP socket, because we want a stream-oriented
connection, that is, one that acts pretty much like a plain old file.
Not all sockets are this of this type.  For example, the UDP protocol
can be used to make a datagram socket, used for message-passing.
<dt class="POD_ITEM"><a name="PeerAddr"><code>PeerAddr</code></a>
<dd>
This is the name or Internet address of the remote host the server is
running on.  We could have specified a longer name like <code>&quot;www.perl.com&quot;</code>,
or an address like <code>&quot;204.148.40.9&quot;</code>.  For demonstration purposes, we've
used the special hostname <code>&quot;localhost&quot;</code>, which should always mean the
current machine you're running on.  The corresponding Internet address
for localhost is <code>&quot;127.1&quot;</code>, if you'd rather use that.
<dt class="POD_ITEM"><a name="PeerPort"><code>PeerPort</code></a>
<dd>
This is the service name or port number we'd like to connect to.
We could have gotten away with using just <code>&quot;daytime&quot;</code> on systems with a
well-configured system services file,[FOOTNOTE: The system services file
is in <i>/etc/services</i> under Unix] but just in case, we've specified the
port number (13) in parentheses.  Using just the number would also have
worked, but constant numbers make careful programmers nervous.
</dl>
<pre class="POD_TEXT">
Notice how the return value from the <code>new</code> constructor is used as
a filehandle in the <code>while</code> loop?  That's what's called an indirect
filehandle, a scalar variable containing a filehandle.  You can use
it the same way you would a normal filehandle.  For example, you
can read one line from it this way:

    $line = &lt;$handle&gt;;
</pre>
<pre class="POD_TEXT">
all remaining lines from is this way:

    @lines = &lt;$handle&gt;;
</pre>
<pre class="POD_TEXT">
and send a line of data to it this way:

    print $handle &quot;some data\n&quot;;
</pre>
<h3 class="POD_HEAD2"><a name="A_Webget_Client">A Webget Client</a></h3>
<pre class="POD_TEXT">
Here's a simple client that takes a remote host to fetch a document
from, and then a list of documents to get from that host.  This is a
more interesting client than the previous one because it first sends
something to the server before fetching the server's response.

    #!/usr/bin/perl -w
    use IO::Socket;
    unless (@ARGV &gt; 1) { die &quot;usage: $0 host document ...&quot; }
    $host = shift(@ARGV);
    $EOL = &quot;\015\012&quot;;
    $BLANK = $EOL x 2;
    foreach $document ( @ARGV ) {
	$remote = IO::Socket::INET-&gt;new( Proto     =&gt; &quot;tcp&quot;,
					 PeerAddr  =&gt; $host,
					 PeerPort  =&gt; &quot;http(80)&quot;,
				        );
	unless ($remote) { die &quot;cannot connect to http daemon on $host&quot; }
	$remote-&gt;autoflush(1);
	print $remote &quot;GET $document HTTP/1.0&quot; . $BLANK;
	while ( &lt;$remote&gt; ) { print }
	close $remote;
    }
</pre>
<p class="POD_TEXT">
The web server handing the &quot;http&quot; service, which is assumed to be at
its standard port, number 80.  If the web server you're trying to
connect to is at a different port (like 1080 or 8080), you should specify
as the named-parameter pair, <code>PeerPort =&gt; 8080</code>.  The <code><a class="POD_LINK" href="perlvar.html#autoflush_HANDLE_EXP">autoflush</a></code>
method is used on the socket because otherwise the system would buffer
up the output we sent it.  (If you're on a Mac, you'll also need to
change every <code>&quot;\n&quot;</code> in your code that sends data over the network to
be a <code>&quot;\015\012&quot;</code> instead.)
</p>
<p class="POD_TEXT">
Connecting to the server is only the first part of the process: once you
have the connection, you have to use the server's language.  Each server
on the network has its own little command language that it expects as
input.  The string that we send to the server starting with &quot;GET&quot; is in
HTTP syntax.  In this case, we simply request each specified document.
Yes, we really are making a new connection for each document, even though
it's the same host.  That's the way you always used to have to speak HTTP.
Recent versions of web browsers may request that the remote server leave
the connection open a little while, but the server doesn't have to honor
such a request.
</p>
<pre class="POD_TEXT">
Here's an example of running that program, which we'll call <i>webget</i>:

    % webget www.perl.com /guanaco.html
    HTTP/1.1 404 File Not Found
    Date: Thu, 08 May 1997 18:02:32 GMT
    Server: Apache/1.2b6
    Connection: close
    Content-type: text/html

    &lt;HEAD&gt;&lt;TITLE&gt;404 File Not Found&lt;/TITLE&gt;&lt;/HEAD&gt;
    &lt;BODY&gt;&lt;H1&gt;File Not Found&lt;/H1&gt;
    The requested URL /guanaco.html was not found on this server.&lt;P&gt;
    &lt;/BODY&gt;
</pre>
<p class="POD_TEXT">
Ok, so that's not very interesting, because it didn't find that
particular document.  But a long response wouldn't have fit on this page.
</p>
<p class="POD_TEXT">
For a more fully-featured version of this program, you should look to
the <i>lwp-request</i> program included with the LWP modules from CPAN.
</p>
<h3 class="POD_HEAD2"><a name="Interactive_Client_w">Interactive Client with IO::Socket</a></h3>
<p class="POD_TEXT">
Well, that's all fine if you want to send one command and get one answer,
but what about setting up something fully interactive, somewhat like
the way <i>telnet</i> works?  That way you can type a line, get the answer,
type a line, get the answer, etc.
</p>
<p class="POD_TEXT">
This client is more complicated than the two we've done so far, but if
you're on a system that supports the powerful <code><a class="POD_LINK" href="perlfunc.html#fork">fork</a></code> call, the solution
isn't that rough.  Once you've made the connection to whatever service
you'd like to chat with, call <code><a class="POD_LINK" href="perlfunc.html#fork">fork</a></code> to clone your process.  Each of
these two identical process has a very simple job to do: the parent
copies everything from the socket to standard output, while the child
simultaneously copies everything from standard input to the socket.
To accomplish the same thing using just one process would be <i>much</i>
harder, because it's easier to code two processes to do one thing than it
is to code one process to do two things.  (This keep-it-simple principle
a cornerstones of the Unix philosophy, and good software engineering as
well, which is probably why it's spread to other systems.)
</p>
<pre class="POD_TEXT">
Here's the code:

    #!/usr/bin/perl -w
    use strict;
    use IO::Socket;
    my ($host, $port, $kidpid, $handle, $line);

    unless (@ARGV == 2) { die &quot;usage: $0 host port&quot; }
    ($host, $port) = @ARGV;

    # create a tcp connection to the specified host and port
    $handle = IO::Socket::INET-&gt;new(Proto     =&gt; &quot;tcp&quot;,
				    PeerAddr  =&gt; $host,
				    PeerPort  =&gt; $port)
	   or die &quot;can't connect to port $port on $host: $!&quot;;

    $handle-&gt;autoflush(1);		# so output gets there right away
    print STDERR &quot;[Connected to $host:$port]\n&quot;;

    # split the program into two processes, identical twins
    die &quot;can't fork: $!&quot; unless defined($kidpid = fork());

    # the if{} block runs only in the parent process
    if ($kidpid) {
	# copy the socket to standard output
	while (defined ($line = &lt;$handle&gt;)) {
	    print STDOUT $line;
	}
	kill(&quot;TERM&quot;, $kidpid);  		# send SIGTERM to child
    }
    # the else{} block runs only in the child process
    else {
	# copy standard input to the socket
	while (defined ($line = &lt;STDIN&gt;)) {
	    print $handle $line;
	}
    }
</pre>
<p class="POD_TEXT">
The <code><a class="POD_LINK" href="perlfunc.html#kill_SIGNAL_LIST">kill</a></code> function in the parent's <code>if</code> block is there to send a
signal to our child process (current running in the <code>else</code> block)
as soon as the remote server has closed its end of the connection.
</p>
<pre class="POD_TEXT">
If the remote server sends data a byte at time, and you need that
data immediately without waiting for a newline (which might not happen),
you may wish to replace the <code>while</code> loop in the parent with the
following:

    my $byte;
    while (sysread($handle, $byte, 1) == 1) {
	print STDOUT $byte;
    }
</pre>
<p class="POD_TEXT">
Making a system call for each byte you want to read is not very efficient
(to put it mildly) but is the simplest to explain and works reasonably
well.
</p>
<h2 class="POD_HEAD1"><a name="TCP_Servers_with_IO_">TCP Servers with IO::Socket</a></h2>
<p class="POD_TEXT">
As always, setting up a server is little bit more involved than running a client.
The model is that the server creates a special kind of socket that
does nothing but listen on a particular port for incoming connections.
It does this by calling the <code>IO::Socket::INET-&gt;new()</code> method with
slightly different arguments than the client did.
</p>
<dl class="POD_LIST">
<dt class="POD_ITEM"><a name="Proto1">Proto</a>
<dd>
This is which protocol to use.  Like our clients, we'll
still specify <code>&quot;tcp&quot;</code> here.
<dt class="POD_ITEM"><a name="LocalPort">LocalPort</a>
<dd>
We specify a local
port in the <code>LocalPort</code> argument, which we didn't do for the client.
This is service name or port number for which you want to be the
server. (Under Unix, ports under 1024 are restricted to the
superuser.)  In our sample, we'll use port 9000, but you can use
any port that's not currently in use on your system.  If you try
to use one already in used, you'll get an &quot;Address already in use&quot;
message.  Under Unix, the <code>netstat -a</code> command will show
which services current have servers.
<dt class="POD_ITEM"><a name="Listen">Listen</a>
<dd>
The <code>Listen</code> parameter is set to the maximum number of
pending connections we can accept until we turn away incoming clients.
Think of it as a call-waiting queue for your telephone.
The low-level Socket module has a special symbol for the system maximum, which
is SOMAXCONN.
<dt class="POD_ITEM"><a name="Reuse">Reuse</a>
<dd>
The <code>Reuse</code> parameter is needed so that we restart our server
manually without waiting a few minutes to allow system buffers to
clear out.
</dl>
<p class="POD_TEXT">
Once the generic server socket has been created using the parameters
listed above, the server then waits for a new client to connect
to it.  The server blocks in the <code><a class="POD_LINK" href="perlfunc.html#accept_NEWSOCKET_GEN">accept</a></code> method, which eventually an
bidirectional connection to the remote client.  (Make sure to autoflush
this handle to circumvent buffering.)
</p>
<p class="POD_TEXT">
To add to user-friendliness, our server prompts the user for commands.
Most servers don't do this.  Because of the prompt without a newline,
you'll have to use the <code><a class="POD_LINK" href="perlfunc.html#sysread_FILEHANDLE_S">sysread</a></code> variant of the interactive client above.
</p>
<p class="POD_TEXT">
This server accepts one of five different commands, sending output
back to the client.  Note that unlike most network servers, this one
only handles one incoming client at a time.  Multithreaded servers are
covered in Chapter 6 of the Camel.
</p>
<pre class="POD_TEXT">
Here's the code.  We'll

 #!/usr/bin/perl -w
 use IO::Socket;
 use Net::hostent;		# for OO version of gethostbyaddr

 $PORT = 9000;			# pick something not in use

 $server = IO::Socket::INET-&gt;new( Proto     =&gt; 'tcp',
                                  LocalPort =&gt; $PORT,
                                  Listen    =&gt; SOMAXCONN,
                                  Reuse     =&gt; 1);

 die &quot;can't setup server&quot; unless $server;
 print &quot;[Server $0 accepting clients]\n&quot;;

 while ($client = $server-&gt;accept()) {
   $client-&gt;autoflush(1);
   print $client &quot;Welcome to $0; type help for command list.\n&quot;;
   $hostinfo = gethostbyaddr($client-&gt;peeraddr);
   printf &quot;[Connect from %s]\n&quot;, $hostinfo-&gt;name || $client-&gt;peerhost;
   print $client &quot;Command? &quot;;
   while ( &lt;$client&gt;) {
     next unless /\S/;	     # blank line
     if    (/quit|exit/i)    { last;                                     }
     elsif (/date|time/i)    { printf $client &quot;%s\n&quot;, scalar localtime;  }
     elsif (/who/i )         { print  $client `who 2&gt;&amp;1`;                }
     elsif (/cookie/i )      { print  $client `/usr/games/fortune 2&gt;&amp;1`; }
     elsif (/motd/i )        { print  $client `cat /etc/motd 2&gt;&amp;1`;      }
     else {
       print $client &quot;Commands: quit date who cookie motd\n&quot;;
     }
   } continue {
      print $client &quot;Command? &quot;;
   }
   close $client;
 }
</pre>
<h2 class="POD_HEAD1"><a name="UDP_Message_Passing">UDP: Message Passing</a></h2>
<p class="POD_TEXT">
Another kind of client-server setup is one that uses not connections, but
messages.  UDP communications involve much lower overhead but also provide
less reliability, as there are no promises that messages will arrive at
all, let alone in order and unmangled.  Still, UDP offers some advantages
over TCP, including being able to &quot;broadcast&quot; or &quot;multicast&quot; to a whole
bunch of destination hosts at once (usually on your local subnet).  If you
find yourself overly concerned about reliability and start building checks
into your message system, then you probably should use just TCP to start
with.
</p>
<p class="POD_TEXT">
Note that UDP datagrams are <i>not</i> a bytestream and should not be treated
as such. This makes using I/O mechanisms with internal buffering
like stdio (i.e. print() and friends) especially cumbersome. Use syswrite(),
or better send(), like in the example below.
</p>
<pre class="POD_TEXT">
Here's a UDP program similar to the sample Internet TCP client given
earlier.  However, instead of checking one host at a time, the UDP version
will check many of them asynchronously by simulating a multicast and then
using select() to do a timed-out wait for I/O.  To do something similar
with TCP, you'd have to use a different socket handle for each host.

    #!/usr/bin/perl -w
    use strict;
    use Socket;
    use Sys::Hostname;

    my ( $count, $hisiaddr, $hispaddr, $histime,
	 $host, $iaddr, $paddr, $port, $proto,
	 $rin, $rout, $rtime, $SECS_of_70_YEARS);

    $SECS_of_70_YEARS      = 2208988800;

    $iaddr = gethostbyname(hostname());
    $proto = getprotobyname('udp');
    $port = getservbyname('time', 'udp');
    $paddr = sockaddr_in(0, $iaddr); # 0 means let kernel pick

    socket(SOCKET, PF_INET, SOCK_DGRAM, $proto)   || die &quot;socket: $!&quot;;
    bind(SOCKET, $paddr)                          || die &quot;bind: $!&quot;;

    $| = 1;
    printf &quot;%-12s %8s %s\n&quot;,  &quot;localhost&quot;, 0, scalar localtime time;
    $count = 0;
    for $host (@ARGV) {
	$count++;
	$hisiaddr = inet_aton($host) 	|| die &quot;unknown host&quot;;
	$hispaddr = sockaddr_in($port, $hisiaddr);
	defined(send(SOCKET, 0, 0, $hispaddr))    || die &quot;send $host: $!&quot;;
    }

    $rin = '';
    vec($rin, fileno(SOCKET), 1) = 1;

    # timeout after 10.0 seconds
    while ($count &amp;&amp; select($rout = $rin, undef, undef, 10.0)) {
	$rtime = '';
	($hispaddr = recv(SOCKET, $rtime, 4, 0)) 	|| die &quot;recv: $!&quot;;
	($port, $hisiaddr) = sockaddr_in($hispaddr);
	$host = gethostbyaddr($hisiaddr, AF_INET);
	$histime = unpack(&quot;N&quot;, $rtime) - $SECS_of_70_YEARS ;
	printf &quot;%-12s &quot;, $host;
	printf &quot;%8d %s\n&quot;, $histime - time, scalar localtime($histime);
	$count--;
    }
</pre>
<p class="POD_TEXT">
Note that this example does not include any retries and may consequently
fail to contact a reachable host. The most prominent reason for this
is congestion of the queues on the sending host if the number of
list of hosts to contact is sufficiently large.
</p>
<h2 class="POD_HEAD1"><a name="SysV_IPC">SysV IPC</a></h2>
<p class="POD_TEXT">
While System V IPC isn't so widely used as sockets, it still has some
interesting uses.  You can't, however, effectively use SysV IPC or
Berkeley mmap() to have shared memory so as to share a variable amongst
several processes.  That's because Perl would reallocate your string when
you weren't wanting it to.
</p>
<pre class="POD_TEXT">
Here's a small example showing shared memory usage.

    use IPC::SysV qw(IPC_PRIVATE IPC_RMID S_IRWXU);

    $size = 2000;
    $id = shmget(IPC_PRIVATE, $size, S_IRWXU) || die &quot;$!&quot;;
    print &quot;shm key $id\n&quot;;

    $message = &quot;Message #1&quot;;
    shmwrite($id, $message, 0, 60) || die &quot;$!&quot;;
    print &quot;wrote: '$message'\n&quot;;
    shmread($id, $buff, 0, 60) || die &quot;$!&quot;;
    print &quot;read : '$buff'\n&quot;;

    # the buffer of shmread is zero-character end-padded.
    substr($buff, index($buff, &quot;\0&quot;)) = '';
    print &quot;un&quot; unless $buff eq $message;
    print &quot;swell\n&quot;;

    print &quot;deleting shm $id\n&quot;;
    shmctl($id, IPC_RMID, 0) || die &quot;$!&quot;;
</pre>
<pre class="POD_TEXT">
Here's an example of a semaphore:

    use IPC::SysV qw(IPC_CREAT);

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 10, 0666 | IPC_CREAT ) || die &quot;$!&quot;;
    print &quot;shm key $id\n&quot;;
</pre>
<pre class="POD_TEXT">
Put this code in a separate file to be run in more than one process.
Call the file <code>take</code>:

    # create a semaphore

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY,  0 , 0 );
    die if !defined($id);

    $semnum = 0;
    $semflag = 0;

    # 'take' semaphore
    # wait for semaphore to be zero
    $semop = 0;
    $opstring1 = pack(&quot;s!s!s!&quot;, $semnum, $semop, $semflag);

    # Increment the semaphore count
    $semop = 1;
    $opstring2 = pack(&quot;s!s!s!&quot;, $semnum, $semop,  $semflag);
    $opstring = $opstring1 . $opstring2;

    semop($id,$opstring) || die &quot;$!&quot;;
</pre>
<pre class="POD_TEXT">
Put this code in a separate file to be run in more than one process.
Call this file <code>give</code>:

    # 'give' the semaphore
    # run this in the original process and you will see
    # that the second process continues

    $IPC_KEY = 1234;
    $id = semget($IPC_KEY, 0, 0);
    die if !defined($id);

    $semnum = 0;
    $semflag = 0;

    # Decrement the semaphore count
    $semop = -1;
    $opstring = pack(&quot;s!s!s!&quot;, $semnum, $semop, $semflag);

    semop($id,$opstring) || die &quot;$!&quot;;
</pre>
<p class="POD_TEXT">
The SysV IPC code above was written long ago, and it's definitely
clunky looking.  For a more modern look, see the IPC::SysV module
which is included with Perl starting from Perl 5.005.
</p>
<pre class="POD_TEXT">
A small example demonstrating SysV message queues:

    use IPC::SysV qw(IPC_PRIVATE IPC_RMID IPC_CREAT S_IRWXU);

    my $id = msgget(IPC_PRIVATE, IPC_CREAT | S_IRWXU);

    my $sent = &quot;message&quot;;
    my $type = 1234;
    my $rcvd;
    my $type_rcvd;

    if (defined $id) {
        if (msgsnd($id, pack(&quot;l! a*&quot;, $type_sent, $sent), 0)) {
            if (msgrcv($id, $rcvd, 60, 0, 0)) {
                ($type_rcvd, $rcvd) = unpack(&quot;l! a*&quot;, $rcvd);
                if ($rcvd eq $sent) {
                    print &quot;okay\n&quot;;
                } else {
                    print &quot;not okay\n&quot;;
                }
            } else {
                die &quot;# msgrcv failed\n&quot;;
            }
        } else {
            die &quot;# msgsnd failed\n&quot;;
        }
        msgctl($id, IPC_RMID, 0) || die &quot;# msgctl failed: $!\n&quot;;
    } else {
        die &quot;# msgget failed\n&quot;;
    }
</pre>
<h2 class="POD_HEAD1"><a name="NOTES">NOTES</a></h2>
<pre class="POD_TEXT">
Most of these routines quietly but politely return <code><a class="POD_LINK" href="perlfunc.html#undef">undef</a></code> when they
fail instead of causing your program to die right then and there due to
an uncaught exception.  (Actually, some of the new <i>Socket</i> conversion
functions  croak() on bad arguments.)  It is therefore essential to
check return values from these functions.  Always begin your socket
programs this way for optimal success, and don't forget to add <b>-T</b>
taint checking flag to the #! line for servers:

    #!/usr/bin/perl -Tw
    use strict;
    use sigtrap;
    use Socket;
</pre>
<h2 class="POD_HEAD1"><a name="BUGS">BUGS</a></h2>
<p class="POD_TEXT">
All these routines create system-specific portability problems.  As noted
elsewhere, Perl is at the mercy of your C libraries for much of its system
behaviour.  It's probably safest to assume broken SysV semantics for
signals and to stick with simple TCP and UDP socket operations; e.g., don't
try to pass open file descriptors over a local UDP datagram socket if you
want your code to stand a chance of being portable.
</p>
<p class="POD_TEXT">
As mentioned in the signals section, because few vendors provide C
libraries that are safely re-entrant, the prudent programmer will do
little else within a handler beyond setting a numeric variable that
already exists; or, if locked into a slow (restarting) system call,
using die() to raise an exception and longjmp(3) out.  In fact, even
these may in some cases cause a core dump.  It's probably best to avoid
signals except where they are absolutely inevitable.  This 
will be addressed in a future release of Perl.
</p>
<h2 class="POD_HEAD1"><a name="AUTHOR">AUTHOR</a></h2>
<p class="POD_TEXT">
Tom Christiansen, with occasional vestiges of Larry Wall's original
version and suggestions from the Perl Porters.
</p>
<h2 class="POD_HEAD1"><a name="SEE_ALSO">SEE ALSO</a></h2>
<p class="POD_TEXT">
There's a lot more to networking than this, but this should get you
started.
</p>
<p class="POD_TEXT">
For intrepid programmers, the indispensable textbook is <i>Unix Network
Programming</i> by W. Richard Stevens (published by Addison-Wesley).  Note
that most books on networking address networking from the perspective of
a C programmer; translation to Perl is left as an exercise for the reader.
</p>
<p class="POD_TEXT">
The IO::Socket(3) manpage describes the object library, and the Socket(3)
manpage describes the low-level interface to sockets.  Besides the obvious
functions in the <a class="POD_LINK" href="perlfunc.html">perlfunc</a> manpage, you should also check out the <code>modules</code> file
at your nearest CPAN site.  (See the <a class="POD_LINK" href="perlmodlib.html">perlmodlib</a> manpage or best yet, the <code>Perl
FAQ</code> for a description of what CPAN is and where to get it.)
</p>
<p class="POD_TEXT">
Section 5 of the <code>modules</code> file is devoted to &quot;Networking, Device Control
(modems), and Interprocess Communication&quot;, and contains numerous unbundled
modules numerous networking modules, Chat and Expect operations, CGI
programming, DCE, FTP, IPC, NNTP, Proxy, Ptty, RPC, SNMP, SMTP, Telnet,
Threads, and ToolTalk--just to name a few.
</p>
<hr>
<a href="#Pod_TOP_OF_PAGE">[Top]</a> 
Generated by Pod::HTML 0.43 on Mon Mar 19 17:35:43 2001
</body>
</html>

